{
	handlers: {
		create: "[set(doc, { users: msg.users, tutorial: msg.tutorial, map_dimensions: msg.map_dimensions, map_lanes: msg.map_lanes, trophies: msg.trophies, starting_units: msg.starting_units, player_resources: msg.player_resources })]",

		start: "if(doc is class game, [
			//already have a game, so recreate/restart.
			set(doc, construct('game', {
				players: map(doc.players, construct('player', {
					player_index: value.player_index,
					name: value.name,
					avatar: value.avatar,
				}))
			})),
			seed_rng(),
		   ], 
		   
		   // create a new game
		   dump('create game: ' + users, [
		      set(doc, construct('game', {
				 starting_units: doc.starting_units,
				 tutorial: if(doc.tutorial != null, construct(doc.tutorial), construct('tutorial')),
		         players: map(users,
				        construct('player',
				            {
								player_index: index,
								name: string<- users[index].user,
								avatar: if(users[index].avatar, users[index].avatar,
								  if(db_info and db_info.info, db_info.info.avatar)
								  where db_info = if(db_client, db_client.get('user:' + users[index].user)))
								  or 'goddess.png',
							}
							+ if(doc.player_resources is int, {base_income: doc.player_resources}, {})
				          ))} + if(doc.map_dimensions is [int,int], {rows: doc.map_dimensions[0], columns: doc.map_dimensions[1], lanes: doc.map_lanes}, {}) +
						        if(doc.trophies is [[int]], {trophies: doc.trophies}, {}))), seed_rng(),
						  
		   ]) where users = [map]<- doc.users)",

		add_bot: "[
		  add(me.state_id, 1),
		  debug('ADD BOT: ' + state_id + ' ' + size(bots)),
		  add(bots, [{
			session_id: session_id,
			script: [{
				session_id: session_id,
				send: {
					type: 'request_updates'
				}
			}],

			on_create: 'set(data, construct(q(bot_' + bot_type + '), {session_id: ${session_id}, args: ' + str(args) + ' or {}} + ( ' + str(bot_args) + ' or {})))',
			on_message: q(data.handle_message(message, me)),

		}]),
		debug('ADD BOT')]",

		message: "[
		 //handle_errors(
		 switch(message.type,
		  'submit_deck',
		  if(message.force or not doc.players[player].deck,
		  [
		  add(me.state_id, 1),
		  set(doc.players, doc.players[:player] + [ new_player ] + doc.players[player+1:]),
		  doc.player_init(new_player)
		  ] where new_player =
		    construct('player',
			  {
				 player_index: player,
				 deck: shuffled_deck[4:],
				 hand: shuffled_deck[0:4],
				 deck_submitted: true,
				 base_income: doc.players[player].base_income + if(player%2 = 0, 0, 1),
				 resources: doc.players[player].base_income + if(player%2 = 0, 0, 1),
				 name: doc.players[player].name,
				 avatar: doc.players[player].avatar,
			  } where shuffled_deck = doc.tutorial.get_deck(player, shuffle(map(message.deck, value)))
			)),
		  'concede', [
			add(me.state_id, 1),
		  	doc.concede(player),
			if(player = 0, set(tbs_game.winner, 1), player = 1, set(tbs_game.winner, 0)),
		  ],
		  'moves', [


		  if(doc.team_index(player) = doc.current_player and message.state_id = state_id, [
			 add(me.state_id, 1),
		     doc.process_moves(map(message.moves, construct('message.' + value.type, value))),

			 bind_command(def(game, tbs_game) if(doc.winner_index != null, set(tbs_game.winner, doc.winner_index)), doc, me),
			],
			 debug('REJECTING MESSAGE FROM PLAYER ' + player + ' STATE ID ' + message.state_id + ' BECAUSE WE ARE PLAYER ' + doc.current_player + ' WITH STATE ID ' + state_id))
		  ]
		),
		 //error handling
		 /*
		 [
		   debug('ERROR WHILE PROCESSING MESSAGE: ' + context.message.type + ': ' + error_msg),
		   context.doc.log_message('Server encountered error when processing ' + message.type)
		 ]
		)
		*/ 
		 ]",

		transform: "[set(message.nplayer, nplayer)]",
	}
}
