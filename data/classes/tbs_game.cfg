{
	create: "def(map msg) ->commands [
		debug(['GGG CREATE', msg]),
		set(game.players, players),
		set(me.setup_commands, string|null<- msg.setup_commands),
		set(_state, game)
	]
		 where players =		 
			 map(msg.users,
			        construct('player',
			            {
							game: game,
							player_index: index,
							name: string<- val.user,
							avatar: if(val.avatar, val.avatar,
							  if(val.account_info,
								(map<- val.account_info).avatar))

							  or 'goddess.png',
						}
						+ if(msg.player_resources is int, {base_income: msg.player_resources}, {})
			          ) where val = map<- msg.users[index]
				) asserting msg.users is list

		  where game =

		      construct('game', {
				 starting_units: null|[map]<- msg.starting_units,
				 tutorial: if(msg.tutorial != null, construct(string<- msg.tutorial), construct('tutorial')),
		         players: [],

				 } + if(msg.map_dimensions is [int,int], {rows: msg.map_dimensions[0], columns: msg.map_dimensions[1], lanes: msg.map_lanes}, {}) +
						        if(msg.trophies is [[int]], {trophies: msg.trophies}, {}))
	
	",

	restart: "def() ->commands [
		debug('START_GAME'),
		set(state.players, players),
		set(_state, game),
		seed_rng();
		debug(['PLAYERS', size(_state.players) asserting _state])
	]
	where players = map(state.players, construct('player', {
				game: game,
				player_index: value.player_index,
				name: value.name,
				avatar: value.avatar,
			}))
	where game = construct('game', {
			players: [],

			starting_units: state.creatures + state.constructs,
			tutorial: state.tutorial,
			rows: state.rows,
			columns: state.columns,
			lanes: state.lanes,
			trophies: state.trophies,
		})
	",

	process: "def() ->commands null",

	message: "def(map message, int player) ->commands

		 //handle_errors(
		 switch(message.type,
		  'submit_deck',
		  if(message.force or not state.players[player].deck,
		  [
		  debug('SERVER GOT submit_deck: ' + str(player)),
		  add(_game.state_id, 1),
		  set(state.players, state.players[:player] + [ new_player ] + state.players[player+1:]),
		  state.player_init(new_player);

		  if(find(state.players, value.deck_submitted = false) = null,
			if(setup_commands, state.execute_commands(setup_commands));
			state.state_based_actions()
		  )
		  ] where new_player =
		    construct('player',
			  {
				 game: state,
				 player_index: player,
				 deck: shuffled_deck[4:],
				 hand: shuffled_deck[0:4],
				 deck_submitted: true,
				 base_income: state.players[player].base_income + if(player%2 = 0, 0, 1),
				 resources: 0,
				 name: state.players[player].name,
				 avatar: state.players[player].avatar,
				 life: state.players[player].life,
			  } where shuffled_deck = state.tutorial.get_deck(player, shuffle(map([string]<- message.deck, value)))
			)),
		  'concede', [
			add(_game.state_id, 1),
		  	state.concede(player),
			if(player = 0, set(_game.winner, 1), player = 1, set(_game.winner, 0)),
		  ],
		  'moves', [


		  if(state.team_index(player) = state.current_player and message.state_id = _game.state_id or message.force, [
			 add(_game.state_id, 1),
		     state.process_moves(map([map]<- message.moves, class message<- construct('message.' + (string<- value.type), map<- value))),

			 bind_command(def(class tbs_game g) if(g.state.winner_index != null, set(_game.winner, g.state.winner_index)), me),
			],
			 debug('REJECTING MESSAGE FROM PLAYER ' + player + ' STATE ID ' + str(message.state_id) + ' BECAUSE WE ARE PLAYER ' + state.current_player + ' WITH STATE ID ' + str(message.state_id)))
		  ]
		)
		 //error handling
		 /*
		 [
		   debug('ERROR WHILE PROCESSING MESSAGE: ' + context.message.type + ': ' + error_msg),
		   context.state.log_message('Server encountered error when processing ' + message.type)
		 ]
		)
		*/ 
	",

	add_bot: "def(int session_id, string bot_type, any args, any bot_args) ->commands [
		add(_game.state_id, 1),
		add(_game.bots, [{
			session_id: session_id,
			script: [{
				session_id: session_id,
				send: {
					type: 'request_updates'
				}
			}],

			on_create: 'set(data, construct(q(bot_' + bot_type + '), {session_id: ' + str(session_id) + ', args: ' + str(args) + ' or {}} + ( ' + str(bot_args) + ' or {})))',
			on_message: q(data.handle_message(message, me)),
		}])
	]",
	
	player_disconnected: "def() ->commands
	[
		add(_game.state_id, 1),
		state.concede(_game.players_disconnected[0]),
		if(_game.players_disconnected[0] = 0, set(_game.winner, 1), _game.players_disconnected[0] = 1, set(_game.winner, 0)),
	]",
	transform: "def(object doc, int nplayer) ->commands [set(doc.nplayer, nplayer)]",
	get_state: "def() ->class game state",

	setup_commands: { type: "null|string" },

	state: "class game<- _state",
	_state: { type: "null|class game" },

	_game: { type: "game" },
}
