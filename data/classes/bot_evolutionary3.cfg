{
	bases: ["bot"],

	constructor: "set(me.rules, file_backed_map(args.rules, def() 1d2000 - 1000,
	   {deck: default_deck})) asserting args.rules",

	properties: {

	score_summons: "def(class game game, map play_info, class card card, Loc loc) ->int
	  rule('summon ' + card.name) +
	  rule('summon ' + if(is_lane_empty, 'lane empty', 'lane not empty')) +

	  rule('summon ' + if('Haste' in creature.abilities, 'haste', 'non-haste') + ' creature in ' + if(is_phase_my_after_combat(game), 'after combat', 'before combat')) +

	  fold(map(friendly_constructs_in_lane(game, lane), rule('summon ' + card.name + ' in lane with friendly construct ' + value.name)), a+b) +
	  fold(map(enemy_constructs_in_lane(game, lane), rule('summon ' + card.name + ' in lane with enemy construct ' + value.name)), a+b) +
	  fold(map(enemy_creatures_in_lane(game, lane),
	        rule('summon ' + card.name + ' vs ' + value.name) +
			if(find(friendly_creatures_in_lane(game, lane), value.loc[1] > context.value.loc[1] = null) = null,
			rule('summon ' + card.name + ' vs unblocked ' + value.name) +
			rule('summon ' + card.name + ' vs unblocked on tile ' + value.tiles_from_start), 0)
		   ), a+b) +
	  fold(map(friendly_creatures_in_lane(game, lane), rule('summon ' + card.name + ' allied with ' + value.name)), a+b) +
	  if(lane_has_neutral_towers(game, lane), rule('summon ' + card.name + ' against neutral') + rule('summon against neutral'), 0) +
	  if(lane_has_neutral_glyphs(game, lane), rule('summon ' + card.name + ' capture glyphs') + rule('summon capture glyphs'), 0) +
	  if(lane_has_enemy_glyphs(game, lane), rule('summon ' + card.name + ' capture enemy glyphs') + rule('summon capture enemy glyphs'), 0)

	  
	where is_lane_empty = lane_empty(game, lane)
	where lane = loc[0]
	where creature = class creature<- card.creature_object",

	spell_targets_loc: "overload(
	  def(class message.play_card msg, Loc loc) ->bool
	    count(msg.targets, loc) > 0,
	  def(class message msg) ->bool false
	)",

	spell_name: "overload(
	  def(class message.play_card msg) ->string|null
	    if(card, card.name) where card = msg.card,
	  def(class message msg) ->string|null null
	)",

	score_spell: "def(class game game, map play_info, class card card, [Loc] targets) ->int
		rule('cast ' + card.name) +
		if(size(targets) = 0, 0, fold([
			if(target_creature = null, 0,
			   if(target_creature.controller = game.current_player_index,
				  rule('cast ' + card.name + ' on friend') +
				  rule('cast ' + card.name + ' on friend ' + target_creature.name) +
				  if(game.stack and spell_targets_loc(game.stack[size(game.stack)-1], target_creature.loc),
				  rule('cast ' + card.name + ' on friend targeted by enemy spell') +
				  rule('cast ' + card.name + ' on friend targeted by ' + spell_name(game.stack[size(game.stack)-1])), 0) ,

				  rule('cast ' + card.name + ' on enemy') +
				  rule('cast ' + card.name + ' on enemy ' + target_creature.name) +
				  if(game.stack and spell_targets_loc(game.stack[size(game.stack)-1], target_creature.loc),
				  rule('cast ' + card.name + ' on enemy targeted by enemy spell') +
				  rule('cast ' + card.name + ' on enemy targeted by ' + spell_name(game.stack[size(game.stack)-1])), 0)
				  )
			   
			)
		], a+b) where target_creature = game.creature_at_loc(targets[0])
			
		)

		+ fold(map(card.ai_patterns(game, targets), rule(card.name + ' ' + value)), a+b)
	",

	best_future_score: "def(class game game) ->int
	  if(is_phase_my_after_combat(game), next_score,
		 max(next_score, best_future_score(next_game)))
		where next_score = if(next_game.current_player_index = next_game.nplayer,
		          if(best_play, int<- best_play.score, 0)
						  where best_play = get_best_play(next_game), 0)
		where next_game = get_modified_object(game, def(class game g) g.end_turn())
	",

	future_game_states: "def(class game game) ->[class game]
		if(game.current_player_index = game.nplayer, [game], []) +
		if(is_phase_my_after_combat(game), [],
		   future_game_states(get_modified_object(game, def(class game g) g.end_turn())))
	",

	calculate_future_plays: "def([class game] states, [class card] hand) ->{class card -> class bot.CardPlay}|null
		fold(filter(map(states, calculate_plays(value, {}, hand)), value != null),
		     {class card -> class bot.CardPlay}<-
			   zip(a, b, if(a.score > b.score, a, b)))
	",

	get_best_play: "def(class game game) -> class bot.CardPlay|null
	 if(plays,
	 if(best_combo and size(best_combo) >= 1,
		if(current_state_play,
		plays[current_state_play] + {discards: filter(map(player.hand, if(value in best_combo, null, index)), value != null)}, null)
		  where current_state_play = find(best_combo, plays[value].game = game),
		  null)
	  where best_combo = choose(combinations, eval_state(simulate_play_cards(game, value)))
	  where player = game.players[game.nplayer]
	  where combinations = playable_combinations(game, plays)
	 )
	  where plays = calculate_future_plays(future_game_states(game), game.player_obj.hand)",

	
	game_states_equal_timing: "def(class game a, class game b) ->bool
		a.current_player_index = b.current_player_index and
		a.nturn = b.nturn and
		a.precombat_phase = b.precombat_phase and
		a.has_unresolved_combat = b.has_unresolved_combat and
		a.in_response_phase = b.in_response_phase
	",

	simulate_play_cards: "def(class game game, [class card] cards) ->class game
		if(cards = [], game,
		  simulate_play_cards(next_game, cards[1:])
		    where next_game = if(play, get_modified_object(game,
			    def(class game g) ->commands
				  g.resolve_card(
				  construct('message.play_card', {
					index: 0,
					player_index: g.current_player_index,
					targets: play.targets,
					card: card,
				  }))
			  ), game)
			where play = calculate_card_play(game, {}, card)
			where card = cards[0]
		)
	",

	simulate_play: "def(class game game, {class card -> class bot.CardPlay} plays, [class card] cards) -> class game
	  if(is_phase_my_after_combat(game),
	     next_game,
		 simulate_play(next_game, plays, next_cards))
	    where next_game = simulate_play_cards(game, play_now)
		where next_cards = filter(cards, value not in play_now)
		where play_now = filter(cards, game_states_equal_timing(plays[value].game, game))
	",


	should_wait: "def(class game game, class bot.CardPlay best_play) ->bool
	  false",
	
	eval_state: "def(class game game) ->int
		rule('EVAL your life ' + game.player_obj.life) +
		rule('EVAL their life ' + game.opponent_obj.life) +
		fold(map(range(game.rows),
		   fold(
			[rule('EVAL ' + friend.name + ' vs ' + enemy.name)
			 | friend <- friends, enemy <- enemies], a+b) +
		   
		   fold(map(friends,
		     rule('EVAL creature ' + value.name) +
		     if(has_neutrals, rule('EVAL ' + value.name + ' vs neutral'), 0) +
			 if(enemies = [], rule('EVAL ' + value.name + ' unblocked') +
					          rule('EVAL ' + value.name + ' unblocked ' + (game.columns - value.tiles_from_start)/max(1, value.move) + ' moves from end'), 0)
		     ), a+b) +
		   
		   fold(map(enemies,
		     rule('EVAL enemy creature ' + value.name) +
		     if(has_neutrals, rule('EVAL enemy ' + value.name + ' vs neutral'), 0) +
			 if(friends = [], rule('EVAL enemy ' + value.name + ' unblocked') +
					          rule('EVAL enemy unblocked ' + (game.columns - value.tiles_from_start)/max(1, value.move) + ' moves from end'), 0)
		     ), a+b) +
			
			if(lane_has_enemy_glyphs(game, value) and friends = [],
			   rule('EVAL uncontested enemy glyphs'), 0) +

			if(enemies and lane_has_my_glyphs(game, value),
			   rule('EVAL undefended glyphs'), 0) +
			
			if(enemy_constructs_in_lane(game, value) and friends = [],
			   rule('EVAL uncontested enemy constructs'), 0) +

			if(enemies and friendly_constructs_in_lane(game, value),
			   rule('EVAL undefended constructs'), 0) +

			if(lane_empty(game, value), rule('EVAL lane empty'), 0)
		     
		   where has_neutrals = lane_has_neutral_towers(game, value)
		   where friends = friendly_creatures_in_lane(game, value)
		   where enemies = enemy_creatures_in_lane(game, value)
		), a+b) +

		fold(map(game.villages, if(value.controller = game.current_player_index, rule('EVAL own village'), if(value.controller = game.next_player_index, rule('EVAL enemy village'), 0))), a+b) +

		fold(map(game.constructs,
			rule('EVAL construct ' + value.name + ' ' + if(value.controller = game.current_player_index, 'yours', 'enemy'))
		), a+b)
	",
	
	deck: "[string]<- rules['deck']",

	default_deck: "[string]<-
		  ['Goblin King']*2 +
		  ['Wolf Rider']*3 +
		  ['Goblin Grenadier']*3 +
		  ['Goblin']*3 +
		  ['Goblin Sapper']*2 +
		  ['Wolf']*2 +
		  ['Green Adder']*2 +
		  [q(Nature's Blessing)]*3 +
		  ['Primal Rage']*3
	",

	rule: "def(string name) ->int  int<- rules[name]",

	rules: { variable: true, type: "object" },
	}
}
