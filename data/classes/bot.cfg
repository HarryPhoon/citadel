{
	properties: {
		session_id: { variable: true, default: 0 },

		args: { variable: true, type: "map", default: {} },

		no_discards: { type: "bool", default: false },

		score_land: "def(class game_state game, map play_info, class card card, Loc loc) ->int
		(
			0
		)",

		score_summons: "def(class game_state game, map play_info, class card card, Loc loc) ->int
		 (
		 	//score for there being an unowned glyph with no other creature
			//claiming it.
			if(nearest_unowned_glyph != null,
			   if(find(game.creatures,
					   value.loc[0] = loc[0] and
					   value.loc[1] >= nearest_unowned_glyph[1]) = null,
					   nearest_unowned_glyph[1]*10, 0), 0)
		 )
			  where nearest_glyph = choose([village.loc | village <- game.villages, village.loc[0] = loc[0]], value[1])
			  where nearest_unowned_glyph = choose([village.loc | village <- game.villages, village.loc[0] = loc[0], village.controller = -1], value[1])
		",

		score_spell: "def(class game_state game, map play_info, class card card, [Loc] targets) ->int card.ai_score(game, targets)",

		score_play: "def(class game_state game, map play_info, class card card, [Loc] targets) if(card.creature and size(targets) = 1,
		   if(card.creature.is_construct,
		    score_land(game, play_info, card, targets[0]),
		    score_summons(game, play_info, card, targets[0])
		   ),
		  score_spell(game, play_info, card, targets))",

		calculate_card_play: "def(class game_state game, map play_info, class card card, [Loc] current_targets=[]) -> class bot.CardPlay|null
		  if(targets = [], null,
			if(targets = null, construct('bot.CardPlay', {
					card: card,
					targets: current_targets,
					score: score_play(game, play_info, card, current_targets),
					game: game,
				}),
				calculate_card_play(game, play_info, card, current_targets + [best_target])
			  where best_target = Loc<- choose(targets, score_play(game, play_info, card, current_targets + [value]))
			))
		  where targets = card.possible_targets(game, game.players[game.nplayer].creature_avatar, current_targets)
		",

		calculate_discard: "def(class game_state game) -> int|null

		if(game.in_response_phase = false and size(player.hand) > player.calculate_card_draw(game) - 2,
			if(most_expensive != null and player.calculate_cost(most_expensive) > player.calculate_income(game)+1,
				index(player.hand, most_expensive))
			where most_expensive = choose(player.hand, player.calculate_cost(value))
		)
		
		where player = game.players[game.nplayer]",

		get_best_play: "def(class game_state game) -> class bot.CardPlay|null
		 if(game.in_unrespondable_phase = false,
		 if(plays,
		 if(best_combo and size(best_combo) >= 1,
			plays[best_combo[0]], null)
		  where best_combo = choose(combinations, fold(map(value, decimal<- plays[value].score), a+b))
		  where player = game.players[game.nplayer]
		  where combinations = playable_combinations(game, plays)
		 )
		  where plays = calculate_plays(game, play_info)
		  where play_info = calculate_play_info(game)
		 )",

		calculate_plays: "def(class game_state game, map play_info, [class card]|null hand=null)
		           ->{class card -> class bot.CardPlay}|null
		    fold(filter(map(playable_cards(game, hand), if(info, {(value): info}, null) where info = calculate_card_play(game, play_info, value)), value != null), a+b asserting a and b)",

		calculate_play_info: "def(class game_state game) ->map {}",

		playable_combinations: "def(class game_state game, map plays_map, [class card] play=[], int existing_cost=0) -> [[class card]]
		   map(candidates, play + [value]) + fold(map(candidates, playable_combinations(game, plays_map, play + [value], existing_cost + player.calculate_cost(value))), a+b)
		   where candidates = [class card]<- filter(player.hand[starting_index:],
		    (not (value in play)) and
		    (player.resources - existing_cost) >= player.calculate_cost(value) and
			plays_map[value] != null and
			value.possible_targets(game, game.players[game.nplayer].creature_avatar, []) != [] and
			value.is_usable(game, game.players[game.nplayer].creature_avatar))
		   where starting_index = if(play = [], 0, index(player.hand, play[size(play)-1])+1)
		   where player = game.players[game.nplayer]
		",

		playable_cards: "def(class game_state game, [class card]|null hand=null) ->[class card]
		  filter(player_hand,
		   player.resources >= player.calculate_cost(value) and
		   (value.is_response or (not game.in_response_phase)) and
		   value.possible_targets(game, game.players[game.nplayer].creature_avatar, []) != [] and
		   value.is_usable(game, game.players[game.nplayer].creature_avatar))
		        where player_hand = if(hand != null, hand, player.hand)
		        where player = game.players[game.nplayer]",

		playable_abilities: "def(class game_state game) ->[{creature: class creature, ability: class card_base}]
			fold([map(filter(creature.activated_abilities, player.resources >= player.calculate_cost(value) and (value.is_response or (not game.in_response_phase)) and (value.uses_action = false or creature.has_action) and value.possible_targets(game, creature, []) != []), {creature: creature, ability: value}) | creature<- game.creatures + game.constructs, creature.controller = game.nplayer], a+b)
			where player = game.players[game.nplayer]
			
		",

		//utilities
		lane_has_neutral_towers: "def(class game_state game, int nlane) ->bool find(game.creatures, value.loc[0] = nlane and value.controller = -1) != null",

		lane_has_neutral_glyphs: "def(class game_state game, int nlane) ->bool find(game.constructs, value.loc[0] = nlane and value.controller = -1) != null",
		lane_has_enemy_glyphs: "def(class game_state game, int nlane) ->bool find(game.constructs, value.loc[0] = nlane and value.controller != -1 and value.controller != game.nplayer) != null",
		lane_has_my_glyphs: "def(class game_state game, int nlane) ->bool find(game.constructs, value.loc[0] = nlane and value.controller = game.nplayer) != null",

		lane_empty: "def(class game_state game, int nlane) find(game.creatures, value.loc[0] = nlane) = null",
		enemy_creatures_in_lane: "def(class game_state game, int nlane) ->[class creature] filter(game.creatures, value.loc[0] = nlane and value.controller != -1 and value.controller != game.current_player_index)",
		friendly_creatures_in_lane: "def(class game_state game, int nlane) ->[class creature] filter(game.creatures, value.loc[0] = nlane and value.controller = game.current_player_index)",
		enemy_constructs_in_lane: "def(class game_state game, int nlane) ->[class creature] filter(game.constructs, value.loc[0] = nlane and value.controller != -1 and value.controller != game.current_player_index)",
		friendly_constructs_in_lane: "def(class game_state game, int nlane) ->[class creature] filter(game.constructs, value.loc[0] = nlane and value.controller = game.current_player_index)",

		is_phase_my_combat: "def(class game_state game) ->bool
		  not game.is_final_response and
		  (game.nplayer = game.current_player_index)",

		is_phase_their_combat: "def(class game_state game) ->bool
		  not game.is_final_response and
		  (game.nplayer != game.current_player_index)",

		is_phase_my_before_combat: "def(class game_state game) ->bool
		  (not game.in_response_phase) and game.has_unresolved_combat",

		is_phase_after_combat: "def(class game_state game) ->bool
			(not game.in_response_phase) and game.has_unresolved_combat = false
		",

		is_phase_my_after_combat: "def(class game_state game) ->bool
		  (not game.in_response_phase) and game.has_unspent_moves = false and game.has_unresolved_combat = false and
		  game.nplayer = game.current_player_index",

		is_in_response_to_their_spell: "def(class game_state game) ->bool
		  game.stack != []",

		request_updates: "def(object bot, int|null state_id=null) -> commands
		[
		  log('BOT: request_updates: ' + str(session_id)),
		  add(bot.script, [{
			session_id: session_id,
			send: {
				type: 'request_updates',
				state_id: if(state_id != null, state_id, _last_state_id),
				allow_deltas: false,
			}
		  }])
		]
		",

		_last_state_id: { default: -1, type: "int" },

		handle_message: "def(map message, object bot) ->commands
		[
			log('BOT: FFL message type: ' + str(message.type)),
			if(message.type = 'game',
			   handle_game_message(message, bot),
			   request_updates(bot)

			)
		]
		",

		_submitted_deck: { type: "bool", default: false },

		handle_game_message: "def(class message.game message, object bot) ->commands 
		instrument('bot_handle_game_message',
		[
		  set(_last_state_id, message.state_id),
		  debug(['BOT: handle game message: ' + str(message.state_id), if(message.state, message.state.current_player_index)]),
		  if(message.state.players[message.state.nplayer].deck_submitted = false asserting message.state.players | message,
			[
			log(['BOT CONSIDER SUBMIT ', _submitted_deck, message.started]),
			//submit our deck to the server.
			if(_submitted_deck = false,
				[
				log('BOT SUBMIT DECK'),
				add(bot.script, [{
					session_id: session_id,
					send: {
						type: 'submit_deck',
						deck: deck,
						player_info: args + {is_ai: true},
					}
				}]),
				set(_submitted_deck, true),
				],

				request_updates(bot, message.state_id)
			),
			],

			find(message.state.players, value.deck_submitted = false) != null,
			request_updates(bot, message.state_id),


			message.state.players[message.state.nplayer].mulligan asserting message.state.players | message,

			[
				add(bot.script, [{
					session_id: session_id,
					send: {
						type: 'mulligan',
						cards: [],
					}
				}])
			],

			if(choice and choice.player_index = message.state.nplayer,
			send_moves(message, bot, [{
				type: 'make_choice',
				choice: 0,
				choice_index: 0,
			}]),

			if(message.state.free_ability != null and message.state.free_ability.nplayer = message.state.nplayer,
			   handle_free_ability(message, bot),
			   message.state.nplayer = message.state.current_player,
			   handle_game_message_internal(message, bot),
			   request_updates(bot, message.state_id)

			)) where choice = message.state.current_choice)
		])",

		handle_free_ability: "def(class message.game message, object bot) ->commands
		(
		if(best_targets != null,
			send_moves(message, bot,
			[
				{
					type: 'play_card',
					index: -1,
					player_index: message.state.nplayer,
					targets: best_targets,
					creature: free_ability.creature.summon_id,
					ability: free_ability.ability.name,
					free_ability: true,
				}
			]),

			send_moves(message, bot,
			[
				{
					type: 'end_turn',
					player_index: message.nplayer,
					keep: []
				}
			])
		)
			where best_targets = calculate_targets_for_free_ability(message.state, free_ability.creature, free_ability.ability, if(free_ability.mandatory, true, false))
		)
			asserting free_ability != null
			where free_ability = message.state.free_ability
		",

		calculate_targets_for_free_ability: "def(class game_state game, class creature creature, class card_base ability, bool is_mandatory) -> null|[Loc]
			null
		",

		handle_game_message_internal: "def(class message.game message, object bot) ->commands
		[
		    if(summons and summons.score > 0 and (not should_wait(message.state, summons)),
				if(summons.card != null,
				  send_moves(message, bot,
					[
						{
							type: 'play_card',
							index: hand_index,
							player_index: message.state.nplayer,
							targets: summons.targets,
						}
					]
				where hand_index = (index(player.hand, summons.card) asserting find(player.hand, value = summons.card) | summons)
				  ),

				  send_moves(message, bot,
				  [
				  	{
						type: 'play_card',
						player_index: message.state.nplayer,
						creature: summons.creature.summon_id asserting summons.creature != null,
						ability: summons.ability.name asserting summons.ability != null,
						targets: summons.targets,
					}
				  ])
				  asserting summons.ability
				 ),

				  if(discard_card != null,
				     send_discard(message, bot, discard_card),
				     end_turn(message, bot)
				  ) where discard_card = if(message.state.in_response_phase = false, calculate_discard(message.state))
				  )
				where summons = get_best_play(message.state)
				where player = message.state.players[message.state.nplayer]
			]
		",

		should_wait: "def(class game_state game, class bot.CardPlay best_play) ->bool
		  if(is_phase_my_after_combat(game),
			 false,
			 if(card is class card_base, choose_to_wait(game, card), false)
			 where card = if(best_play.card is class card, best_play.card, best_play.ability)
		  )
		",

		choose_to_wait: "def(class game_state game, class card_base card) ->bool
		  true
		",

		send_discard: "def(class message.game message, object bot, int card_index) ->commands send_moves(message, bot, [{
			type: 'discard_card',
			player_index: message.state.nplayer,
			index: card_index,
		}])",

		end_turn: "def(class message.game message, object bot) ->commands send_moves(message, bot, [{type: 'end_turn', player_index: message.nplayer, keep: []}])",

		send_moves: "def(class message.game message, object bot, [map] moves) ->commands [
				  add(bot.script, [{
					session_id: session_id,
					send: {
						type: 'moves',
						state_id: message.state_id,
						moves: moves
					}
				  }])
		]",

		deck: "[string]<- get_document('data/ai-deck.cfg')"
	},

	classes: {
		CardPlay: {
			card: { type: 'class card|null' },
			creature: { type: 'class creature|null' },
			ability: { type: 'class card_base|null' },
			targets: { type: '[Loc]' },
			score: { type: 'int' },
			game: { type: 'class game_state' },
		}
	},
}
