{
	location_completed_encounters: "def(class campaign_location loc) ->int
		size(filter(loc.regular_encounters, lib.citadel.get_campaign_encounter_victories(value.uuid) > 0))
	",

	location_total_encounters: "def(class campaign_location loc) ->int
		size(loc.regular_encounters)
	",

	//Called when we want to generate a new encounter. Right now we
	//just choose a random scenario from the list of scenarios in
	//data/encounters/encounters.cfg.
	//In the future we want to do things like avoid
	//repeating the same encounter, look at the user's account details
	//for level info etc. But for now it can be simple.
	generate_encounter: "def(obj title_controller controller, class campaign_location loc) ->class campaign_encounter
	if(MODULE_OPTIONS.campaign_encounter,
		find_or_die(possible_encounters, value.title = MODULE_OPTIONS.campaign_encounter),

		available_encounters,

		construct('campaign_encounter', {
			title: chosen.uuid,
			node: chosen.dialog_node,
		}) where chosen = choose(available_encounters),

		//OLD WAY OF CHOOSING ENCOUNTERS -- DEPRECATED
		result

		where result = get_modified_object(choose(options), def(class campaign_encounter e)->commands null)
		where options = mundane_encounters*2 + quest_encounters*(1+_streak_mundane_encounters)
		where quest_encounters = filter(filtered_encounters, value.quest_id != null)
		where mundane_encounters = if(MODULE_OPTIONS.campaign_quest != null, [], filter(filtered_encounters, value.quest_id = null))
		where filtered_encounters =
		  filter(possible_encounters,
		       (MODULE_OPTIONS.campaign_quest = null or MODULE_OPTIONS.campaign_quest = value.quest_id) and
		       (
		         value.quest_id = null or
				 (
				  progress >= value.min_quest_progress and progress <= value.max_quest_progress
				  
				  where progress = (lib.citadel.campaign_quest_progress[value.quest_id] or 0)
				 )
		       )
			   and value.filter_encounter()

			   and (value.once_only = false or
			        lib.citadel.get_campaign_encounter_victories(value.title) = 0)

			   and (value.prerequisite = null or
			        lib.citadel.get_campaign_encounter_victories(value.prerequisite) > 0)

			   and (value.location = null or
			        (value.location is string and value.location = loc.name) or
					(value.location is [string] and loc.name in value.location)
				   )
		  )
	)

	where available_encounters = filter(filtered_encounters, lib.citadel.get_campaign_encounter_victories(value.uuid) = least_victories)
	where least_victories = min(map(filtered_encounters, lib.citadel.get_campaign_encounter_victories(value.uuid)))
	where filtered_encounters = filter(loc.regular_encounters, find(value.prerequisites, lib.citadel.get_campaign_encounter_victories(value) = 0) = null and lib.campaign_encounter.eval_condition(controller, value.condition))
	",

	_streak_mundane_encounters: { default: 0, type: "int" },

	record_enter_encounter: "def(class campaign_encounter encounter) ->commands [
		if(encounter.quest_id = null,
		   add(_streak_mundane_encounters, 1),
		   set(_streak_mundane_encounters, 0)
		)
	]",

	possible_encounters: "[class campaign_encounter] :: query_cache(global_cache(1), null, map([map]<- get_document('data/encounters/encounters.cfg'), construct('campaign_encounter', value)))",
}
