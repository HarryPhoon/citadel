{
	server_only: true,

properties: {
	matchmake: "def([MatchmakingInfo] info) ->[int]|null
	  if(value != null,
		[first_index, second_index]
	    where second_index = index(info[first_index+1:], sorted[index(sorted, value)+1]) + first_index + 1
	    where first_index = index(info, value)
      )
	    where value = find(sorted, index+1 < size(sorted) and sorted[index+1].type = value.type)
		where sorted = sort(info, a.type < b.type)
	",

	new_account_cards: "{string -> int} ::
	  fold(map(new_cards /*lib.citadel.get_cards('core')*/, {(value): 3}), a+b)

	  where new_cards = ['Disciple', 'Dawn Obelisk', 'Heal', 'Inquisition', q(Rihn's Anointed), q(Rihn's Martyr), 'Rokh', 'Smite the Defilers', 'Temple Guard', 'Weakness',
	     'Cover', 'Darkwood Denizen', 'Furious Rampage', 'Green Adder', 'Wolf Rider']
	",

	create_account: "def(map msg) ->AccountDetails
	{
		collection:
		fold(map(new_account_cards, map(range(value), {
			id: generate_uuid(),
			name: card_name,
		}) where card_name = key), a+b),

		avatar: 'endless-studies.png',
		unlocks: ['CAMPAIGN'],
		campaign_progress: {},
		decks: DeckMap<- get_document('data/default-user-decks.cfg'),
		quests: [],
	}
	",

	read_account: "def(map info) ->AccountDetails
		lib.citadel.construct_account(info)
	",

	_grant_cards: "def(matchmaking_server server, string user_id, AccountDetails account_info, [string] sets) ->commands [
		[

		map(new_cards,
		   if(current_count < 3,
				map(range(3 - current_count), add(account_info.collection, [lib.citadel.generate_card_instance(context.value)]))
		   )	
		   where current_count = count(account_info.collection, value.name = context.value))
		
		where new_cards = lib.citadel.get_cards_in_set(set) | set <- sets],

		server.write_account(user_id)
	]",

	user_account: "def(matchmaking_server server, string user_id, AccountDetails account_info, map msg) ->commands
	if(type = 'grant',
		_grant_cards(server, user_id, account_info, sets)
		where sets = [string]<- msg['sets'] or ['core']
	)
		where type = string<- msg['cmd_type']
	",

	admin_account: "def(matchmaking_server server, string user_id, AccountDetails account_info, map msg) ->commands
	if(type = 'grant',
		_grant_cards(server, user_id, account_info, sets)

		where sets = [string]<- msg['sets']
	)
	where type = string<- msg['type']
	",

	_assign_quests: "def(AccountDetails account_info, [int] dates) ->[Quest]
		map(dates, {
			assigned_date: value,
			type: 'quest',
			args: {
				name: 'test_quest',
				campaign: choose(keys(account_info.campaign_progress)) or 'Training Grounds',
			},
		})
	",

	process_account: "def(matchmaking_server server, string user_id, AccountDetails account_info) ->commands
	[
		debug('XXX process_account'),
		if(new_quests != account_info.quests, [
			set(account_info.quests, new_quests),
			server.write_account(user_id),
		])
	]
	where new_quests = filter(account_info.quests, value.assigned_date + 3 >= current_date) +
	  _assign_quests(account_info, map(filter([2,1,0], find(account_info.quests, value.assigned_date = current_date - context.value) = null),
	     current_date - value
	     ))
	where current_date = (t.year - 2015)*365 + t.yday
	//where current_date = t.hour*60*60 + t.minute*60 + t.second
	where t = time()
	",

	handle_game_over_message: "def(matchmaking_server server, map msg) ->commands [
	]",

	handle_anon_request: "def(matchmaking_server server, map msg) ->map|list|null
	if(msg['type'] = 'deck' and msg.user is string,
		if(account_info = null,
		   { type: 'error', msg: 'no such user' },

		   if(deck is map,
			  list<- deck.cards,
		      { type: 'error', msg: 'no such deck' }
		   )

		   where deck = decks[msg['deck']]
		   where decks = map<- info['decks']
		   where info = map<- account_info['info']
		   asserting account_info is map
		)
		where account_info = server.db_client.get('user:' + (string<- msg['user']))
	)	
	",

	handle_request: "def(matchmaking_server server, map msg, string user_id, AccountDetails account_info) ->commands
	  switch(msg.type,
			'set_avatar',
			[
			set(account_info.avatar, string<- msg.avatar),
			server.write_account(user_id),
			],

			'set_campaign_location',

			[
			set(account_info.campaign_location, string<- msg.campaign_location),
			server.write_account(user_id),
			],

			'set_decks',

			[
			set(account_info.decks, DeckMap<- msg.decks),
			server.write_account(user_id),
			set(server.response, {type: 'ok'})
			],

			'finish_bot_game',
			[
			add(account_info.collection, reward_cards),

			if(msg.unlock is string and find(account_info.unlocks, value = msg.unlock) = null,
			   add(account_info.unlocks, [string<-  msg.unlock])),

			if(msg.campaign_scenario is int and msg.campaign_scenario >= (account_info.campaign_progress[msg.campaign_location] or 0),
			[
			   set(account_info.campaign_progress[msg.campaign_location], (account_info.campaign_progress[msg.campaign_location] or 0) + 1)
			]
			),

			//write silently as our response should let the client
			//update on their own just fine.
			server.write_account(user_id, ['silent']),

			set(server.response, {type: 'new_cards', cards: reward_cards }),

			] where reward_cards = if(msg.victory = true and msg.campaign_sets,
			   map(calculate_card_rewards(account_info, [string]<- msg.campaign_sets), lib.citadel.generate_card_instance(value)), []),

			set(server.response, {type: 'error', message: 'Unknown request type'})
	  )
	",

	calculate_card_rewards: "def(AccountDetails account, [string] sets) ->[string]

	map(card_instances, value.name)
	where card_instances =
		if(candidates_common,
		   candidates_common[:4] + if(1d3 = 1, candidates_uncommon[:1] + candidates_rare[:1], candidates_uncommon[:2]),
		   candidates_uncommon[:2] + candidates_rare[:1])

		where candidates_rare =
		  shuffle(filter(candidates, value.rarity = 2))
		where candidates_uncommon =
		  shuffle(filter(candidates, value.rarity = 1))
		where candidates_common =
		  shuffle(filter(candidates, value.rarity = 0))
		where candidates =
		  fold(map(sets, filter(lib.citadel.get_card_objects_in_set(value), count(account.collection, value.name = context.value.name) < 3)), a+b)
	",
}
}
