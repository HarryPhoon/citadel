{
	constructor: "[
		if(doc, add(me.creatures,
			map(doc, construct('creature', (map<- value) + {summon_id: 10+index})))
		) where doc = get_document('data/starting-creatures.cfg', ['null_on_failure']),

		add(me.constructs, filter(starting_creatures, value.is_construct)),
		add(me.creatures, filter(starting_creatures, not value.is_construct)),

		set(starting_units, null),

		bind_command(_calculate_resource_levels),
	   
	  ]
	   where starting_creatures =
	     if(starting_units is [class creature], starting_units,
		  map(units, lib.citadel.create_creature(string<- value.card_name, { summon_id: 10 + index, controller: -1 } + value))
		)
	   where units = if(starting_units is [map], starting_units, [
	     { card_name: 'Village', loc: [0, 2] },
	     { card_name: 'Village', loc: [2, 1] },
	     { card_name: 'Village', loc: [2, 3] },
	   ])",

	properties: {
		starting_units: { type: "null|[map]|[class creature]" },
		tutorial: { type: 'class tutorial', initialize: "construct('tutorial')" },

		nplayer: { variable: true, default: 0, type: 'int' },
		nteam: "int :: nplayer%2",
		players: { variable: true, type: '[class player]' },
		team: "def(class player p) ->[class player]
			filter(players, (n%2) = (index%2)) where n = index(players, p)
		",

		players_on_team: "def(int nteam) ->[class player]
			filter(players, nteam = (index%2))",

		team_index: "def(int n) ->int
			n%num_teams
		",

		num_teams: "int :: 2",

		current_player: "int <- current_player_index", // deprecated due to poor naming
		current_player_index: "int <- if(current_choice != null, current_choice.player_index, (nturn + precombat_phase + size(stack))%num_teams)",
		current_team_index: "team_index(current_player_index)",
		current_player_turn_index: "int :: nturn%2",
		player_obj: "class player <- players[current_player_index]",
		opponent_obj: "class player <- players[next_player_index]",
		next_player_index: "(current_player_index+1)%num_teams",
		next_player: "(current_player_index+1)%num_teams",

		is_day: "bool :: (nturn/2)%2 = 0",
		is_night: "bool :: not is_day",

		villages: "[class creature] :: [village | village <- constructs, village.name = 'Village']",

		player_init: "def(class player player) if(player.player_index = 0, player.begin_turn(me))",

		current_choice: { variable: true, default: null, type: 'class choice|null' },

		creature_summon_id: { type: 'int', default: 100, access: 'private' },
		nturn: { type: 'int', default: 0 },

		turn: "int <- nturn",
		precombat_phase: { variable: true, default: 0 },
		response_played: { variable: true, default: false },

		stack: { variable: true, default: [], type: "[class message.play_card_base]" },
		stack_id: { variable: true, default: 1 },

		in_response_phase: "precombat_phase != 0 or stack != []",

		log_message: "def(string msg) ->commands add(me.log, [msg])",
		log: { default: [], type: '[string]' },

		endzoned_creatures: { type: "[int]", default: [] },
		creatures: { variable: true, default: [], type: "[class creature]" },
		creatures_and_avatars: "[class creature] <- creatures + map(players, value.creature_avatar)",
		permanents: { get: "creatures + constructs", type: "[class creature]" },
		permanents_and_avatars: { get: "creatures + constructs + map(players, value.creature_avatar)", type: "[class creature]" },

		constructs: { variable: true, default: [], type: "[class creature]" },

		//dead creatures, keyed by summon_id.
		graveyard: { variable: true, default: {}, type: "{int -> class creature}" },

		capture: "def(Loc loc, int owner) ->commands [
		  if(construct and construct.controller != owner,
			if('Fragile' in construct.abilities,
			 set(construct.destroyed, true),
			 set(construct.controller, owner)))
		      where construct = construct_at_loc(loc)
		]",

		rows: { variable: true, default: 5},
		columns: { variable: true, default: 5},

		lanes: { type: "[MapLane]", default: [
			{begin: 1, length: 3},
			{begin: 0, length: 4},
			{begin: 0, length: 5},
			{begin: 0, length: 4},
			{begin: 1, length: 3},
		]
		},

		animation_hints: { variable: true, type: '[class animation.hint]', default: [] },
		animation_hints_id: { variable: true, type: 'int', default: 1 },
		
		winner: "if(count(players[:num_teams], value.life > 0) = 1, find(players, value.life > 0), null)",
		winner_index: "if(count(players[:num_teams], value.life > 0) = 1,
		   if(players[0].life > 0, 0, if(players[1].life > 0, 1, -1)), null)",

		concede: "def(int nplayer) ->commands [
			set(players[team_index(nplayer)].life, 0)
		]",

		clear_transient_state: "def() ->commands [
			[set(c.movement_log, []) | c <- creatures, c.movement_log != []]
		]",

		process_moves: "def([class message] moves) [
		  clear_transient_state(),
		  map(moves, bind_command(process_game_move, value))
		]
		",

		process_game_move: "overload(
		  def(class message.debug msg)        process_debug(msg),
		  def(class message.make_choice msg)  make_choice(msg),
		  def(class message.discard_card msg)    discard_card(msg),
		  def(class message.play_card msg)    play_card(msg),
		  def(class message.play_ability msg) play_ability(msg),
		  def(class message.end_turn msg)     handle_end_turn_message(msg.player_index),
		  def(class message.movement msg)     movement(msg),
		)",

		has_unresolved_combat: "bool ::
		  find(creatures, size(value.targets_attacking(me))) != null",

		process_debug: "def(class message.debug info) [
			log_message('PROCESS DEBUG MESSAGE'),
			if(info.exe, if(result, result, log_message('failed to eval debug expression')) where result = eval(info.exe))
		]",

		resolve_ability: "def(class message.play_ability info) [
		if(creature != null and info.ability_obj.targets_valid(me, creature, info.targets) and info.force_fizzle = false, [
			debug('resolve ability'),
			info.ability_obj.on_play(me, creature, info),
			state_based_actions(),
			if(info.stack_id, add(me.animation_hints, [construct('animation.hint', {
				type: 'resolve',
				stack_id: info.stack_id,
			})]))
		],
		if(info.stack_id, add(me.animation_hints, [construct('animation.hint', {
			type: 'fizzle',
			stack_id: info.stack_id,
		})]))
		),

		if(creature != null, info.ability_obj.on_pay_cost(me, creature, info)),
		]
		asserting info.ability_obj
		where creature = permanent_by_summon_id(info.creature)
		",

		play_ability: "def(class message.play_ability info) [
		if(creature and ability and
		   ((not ability.exhausts_creature) or (not creature.is_exhausted)) and
		   (not creature.is_sleeping), ([
		  player.spend(ability),
		  if(ability.exhausts_creature, creature.set_exhausted()),

		  //resolve_ability(play_info)
		  add(me.stack_id, 1),
		  (add(me.stack, [play_info])
		    where play_info = info + {
				ability_obj: ability,
				stack_id: stack_id,
		    }),

		  add(me.animation_hints, [construct('animation.play_card_hint', {
			type: 'play_ability',
			card: ability,
			player: current_player,
			stack_id: stack_id,
			targets: [creature.loc] + info.targets
		  })]),
		  state_based_actions(),
		  set(me.response_played, true)
		] asserting player.can_afford(ability),
		  (not in_response_phase) or ability.is_response),
		  if(creature and ability,
		     ability.targets_valid(me, creature, info.targets))
		 )
		  where ability = if(creature, find(creature.activated_abilities, value.name = info.ability))
		  where creature = find(permanents, value.summon_id = info.creature)
		  where player = players[current_player]
		]",

		set_current_choice: "def(map info) ->commands
			set(me.current_choice, construct('choice', info + if(info.player_index = null, {player_index: current_player}, {})))
		",

		make_choice: "def(class message.make_choice info) if(current_choice, [
			set(me.current_choice, null),

			if(current_choice.card != null, bind_command(current_choice.card.on_play, me, current_choice.info + {choices: (list<- current_choice.info.choices or []) + [info.choice]}) asserting current_choice.card is class card),

			if(current_choice.ability != null, bind_command(current_choice.ability.on_play, me, current_choice.creature asserting current_choice.creature is class creature, current_choice.info + {choices: (list<- current_choice.info.choices or []) + [info.choice]}) asserting current_choice.ability is class activated_ability),

		])",

		resolve_card: "def(class message.play_card info) ->commands [
		    players[info.player_index].level_up(info.card.school),

			if(not info.card.creature,
			   players[info.player_index].discard_visible(me, info.card)),

			//check the targets are still valid, otherwise the spell fizzles.
			if(info.card.targets_valid(me, info.player_index, info.targets) and info.force_fizzle = false,
				[
				   bind_command(info.card.on_play, me, info),

					if(info.stack_id, add(me.animation_hints, [construct('animation.hint', {
						type: 'resolve',
						stack_id: info.stack_id,
					})]))
				],
				
				if(info.stack_id, add(me.animation_hints, [construct('animation.hint', {
					type: 'fizzle',
					stack_id: info.stack_id,
				})]))
			),

			bind_command(info.card.on_pay_cost, me, info),

			state_based_actions(),
		] asserting info.card",

		discard_card: "def(class message.discard_card info) [
			//player.level_up(player.hand[info.index].school),
			player.discard_visible(me, player.hand[info.index]),
			set(player.hand, player.hand[:info.index] + player.hand[info.index+1:]),
		] where player = players[info.player_index]",

		play_card: "def(class message.play_card info) ([
		  log_message(player.name + ' plays ' + player.hand[info.index].name),
		  player.spend(player.hand[info.index]),
		  set(player.hand, filter(player.hand, index != info.index)),

		  player.hand[info.index].on_cast(me, info),

		  map(permanents_and_avatars, value.fire_event('on_card_played', [me, player, info])),

		  if(player.hand[info.index].plays_on_stack,
			 [
		  		player.hand[info.index].record_animation(me, info, stack_id),
		     	add(me.stack, [play_info + {stack_id: stack_id}]),
				add(me.stack_id, 1),
			 ],
		     resolve_card(play_info)) where play_info = info + {card: player.hand[info.index], player_index: info.player_index},

		  set(me.response_played, true),
		  ]
		  asserting info.index >= 0 and info.index < size(player.hand),
		            dump({infoinfo: 'hello', name: player.hand[info.index].name, card_index: info.index, resources: player.resources, cost: player.calculate_cost(player.hand[info.index]), player_index: info.player_index, player_index_me: player.player_index}, player.can_afford(player.hand[info.index])),
					(not in_response_phase) or player.hand[info.index].is_response,
					player.hand[info.index].targets_valid(me, info.player_index, info.targets)
				| [info.index, info.targets, map(player.hand, value.name), in_response_phase])
		  where player = players[info.player_index]",

		set_creature_summoning_id: "def(class creature new_creature) ->commands [
			set(new_creature.summon_id, creature_summon_id),
			add(me.creature_summon_id, 1),
		]",

		summon_creature: "def(class creature new_creature) ->commands [
		  bind_command(set_creature_summoning_id, new_creature),
		  add(me.creatures, [new_creature]),
		  
		  bind_command(new_creature.handle_summoning, me),
		  bind_command(_calculate_resource_levels),
		  state_based_actions(),
		  bind_command(new_creature.exhaust_on_summon, me),

		  bind_command(fire_creature_event, 'on_another_creature_summoned', [new_creature]),

	    ] asserting (not new_creature.is_construct)",

		summon_construct: "def(class creature new_construct) ->commands [
		  bind_command(set_creature_summoning_id, new_construct),
		  add(me.constructs, [new_construct]),

		  bind_command(_calculate_resource_levels),
		  
		  bind_command(new_construct.handle_summoning, me),
		  state_based_actions(),
		  bind_command(new_construct.exhaust_on_summon, me),

		] asserting new_construct.is_construct",

		return_from_graveyard: "def(class creature creature) ->commands
		if(me.graveyard[creature.summon_id], [
			set(me.graveyard, remove_from_map(me.graveyard, creature.summon_id)),
			add(me.creatures, [creature]) asserting (not (creature in me.creatures)),
		    bind_command(_calculate_resource_levels),
		])",

		return_from_graveyard_to_hand: "def(class creature creature) ->commands
		if(target and target.card_name and target.controller >= 0 and target.controller < size(players), [
			set(me.graveyard, remove_from_map(me.graveyard, creature.summon_id)),
			add(me.players[creature.controller].hand, [target.card_name])
			
		]) where target = graveyard[creature.summon_id]",


		remove_static_effects: "def() ->commands map(me.players, value.remove_static_effects()) + map(me.permanents_and_avatars, value.remove_static_effects(me))",
		apply_static_effects: "def() ->commands map(me.permanents_and_avatars, value.apply_static_abilities(me) asserting value.apply_static_abilities != null | [value,index])",

		state_based_construct_destruction: "def() ->commands [
			[capture(creature.loc, if('Marauder' in creature.abilities and not is_cove, -1, creature.controller))
			where is_cove = if(construct != null, 'Cove' in construct.abilities, false) where construct = construct_at_loc([creature.loc[0], creature.loc[1]])
			| creature <- me.creatures],
		]",

		state_based_actions_internal: "def() ->commands [
		    set(me.creatures, new_creatures),
			set(me.constructs, new_constructs),
			map(me.creatures, if((not value.alive) and (not value.obliterated), add(me.graveyard, {value.summon_id: value}))),
			[ if(construct.card_name != null,
			     me.players[construct.controller].discard_visible(me, lib.citadel.create_card(construct.card_name))) |
			  construct <- constructs,
			  construct.controller >= 0,
			  not construct.alive ],

			map(me.permanents, if(not value.alive, [
			  bind_command(value.fire_events_on_death, me),
			  bind_command(value.fire_event, 'on_die', [me])
			])),
		 if(new_creatures != me.creatures or new_constructs != me.constructs,
		 [
		 	bind_command(_calculate_resource_levels),
		    bind_command(state_based_actions)
		 ]
		   )
		]
				 
			where new_creatures = filter(me.creatures, value.alive)
			where new_constructs = filter(me.constructs, value.alive)
				 ",

		_calculate_resource_levels: "def() ->commands
			map(players, value.calculate_resource_levels(me))
		",

		state_based_actions: "def() ->commands
		 [
		   bind_command(remove_static_effects),
		   bind_command(apply_static_effects),
		   bind_command(state_based_construct_destruction),
		   bind_command(state_based_actions_internal),
		   bind_command(state_based_moves),
		 ]
		",

		num_moves_made: { type: "int", default: 0 },
		_moves_made_start: { type: "int", default: 0 },

		_state_based_actions_if_moves: "def() ->commands if(num_moves_made != _moves_made_start, state_based_actions())",

		state_based_moves: "def() ->commands
		[
		   set(_moves_made_start, num_moves_made),
		   bind_command(do_moves),
		   bind_command(do_endzone),
		   //bind_command(_state_based_actions_if_moves),
		]",

		do_moves: "def() [
		   map(filter(creatures, value.controller = current_player_turn_index and value.being_summoned = false and not value.is_exhausted),
		       bind_command(value.do_move, me)),
		 ]",


		do_first_strike_attacks: "def()
		   map(filter(creatures, ('First Strike' in value.abilities) and not (value.controller = current_player_turn_index and value.is_exhausted)),
		       value.do_attack(me)
		      )",
		do_attacks: "def()
		   map(filter(creatures, (not ('First Strike' in value.abilities)) and not (value.controller = current_player_turn_index  and value.is_exhausted)),
		       value.do_attack(me)
		      )",

		do_trampling: "def() if(find(creatures, ('Trampling' in value.status_effects)),
		  map(filter(creatures, ('Trampling' in value.status_effects)),
		  [
		    bind_command(value.do_trample_move, me),

		  
		]))",

		//rewards for getting to the end of a lane
		trophies: {
			type: "[[int]]",
			default: [
			  [8, 10, 15, 10, 8],
			  [8, 10, 15, 10, 8],
			],
		},

		apply_trophy: "def(int nplayer, int nlane) ->commands [
			add(players[nplayer].life, -trophies[nplayer][nlane]),
			set(trophies, map(trophies, map(value, if(index = nlane and player_index = nplayer, 0, value)) where player_index = index)),
		]",

		do_endzone: "def() if(find(creatures, is_loc_in_endzone(value.loc, value.controller)) != null, [
		  map(filter(creatures, is_loc_in_endzone(value.loc, value.controller)), [value.fire_event('on_reach_endzone', [me]), add(endzoned_creatures, [value.summon_id])]),
		  set(me.creatures, filter(creatures, not is_loc_in_endzone(value.loc, value.controller))),
		  [add(me.graveyard, {(creature.summon_id): creature}) | creature <- creatures, is_loc_in_endzone(creature.loc, creature.controller)],

		  bind_command(_calculate_resource_levels),
		  state_based_actions(),
		])",

		fire_creature_event: "def(string event_name, list args=[]) ->commands
			map(permanents_and_avatars, value.fire_event(event_name, [me] + args))
		",

		do_begin_turn: "def() map(permanents_and_avatars, value.begin_turn(me))",
		do_end_turn: "def() map(permanents_and_avatars, value.end_turn(me))",

		// any creatures in the graveyard have cards dis-assocated with them,
		// and those cards moved to their owner's discard pile.
		do_cleanup_graveyard: "def() [
			map(me.graveyard, if(value.card_name and value.controller >= 0, [
				if(not value.obliterated, players[value.controller].discard_visible(me, lib.citadel.create_card(value.card_name))),
			])),

			set(graveyard, {}),
		]
		",

		resolve: "overload(
		  def(class message.play_ability item) resolve_ability(item),
		  def(class message.play_card item)    resolve_card(item)
		)",

		handle_end_turn_message: "def(null|int player_index=null) ->commands [
			set(players[if(player_index = null, current_player_index, player_index)].has_ended_turn, true),
			bind_command(me.check_end_turn),
		]",

		check_end_turn: "def() ->commands [
			if(find(players_on_team(current_player_index), value.has_ended_turn = false) = null, end_turn())
		]",

		end_turn: "def() ->commands
		[
		  map(players, set(value.has_ended_turn, false)),

		  if(stack != [], [
			set(me.stack, stack[0:size(stack)-1]),
			bind_command(me.resolve, back(stack)),
		   ],
		   if(has_unresolved_combat or precombat_phase, if((response_played or precombat_phase = 0),
				    [add(me.precombat_phase, 1), set(me.response_played, false)],
				    [set(me.precombat_phase, 0), combat()]),
		   [
		   set(me.animation_hints, []),
		   add(me.animation_hints_id, 1),

		   state_based_actions(),

		   map(players_on_team(nturn%num_teams), value.end_turn(me)),

		   bind_command(do_end_turn),
		   bind_command(do_cleanup_graveyard),

		   state_based_actions(),

		   log_message(players[next_player].name + ', Turn ' + ((me.nturn+1)/num_teams+1)),
			   
		   add(me.nturn, 1),
		   set(me.precombat_phase, 0),
		   set(me.response_played, false),
		   map(players_on_team((nturn+1)%num_teams), value.begin_turn(me)),
		   map(me.permanents, if(value.controller = next_player, value.increment_summoning)),
		   bind_command(do_begin_turn),

		   state_based_actions(),

		   ]))
		]",

		movement: "def(null|class message info=null) ->commands
		   [
				   /*
			 bind_command(do_moves),
		     state_based_actions(),
			 bind_command(do_endzone),
		     state_based_actions(),
			 */

		      add(me.precombat_phase, 1),
		      set(me.response_played, false)
		   ]
		",

		combat: "def() ->commands [

		   //first strike combat
		   if(find(creatures, 'First Strike' in value.abilities),
			[
			  bind_command(do_first_strike_attacks),
		      state_based_actions()
			]),
		   
		   //regular combat.
		   bind_command(do_attacks),

		   state_based_actions(),

		   bind_command(do_trampling),
		   state_based_actions(),
		   bind_command(do_endzone),
		]
		",

		//gets adjacent on-board locs. TODO: make this more efficient.
		get_adjacent_locs: "def(Loc loc) -> [Loc]
			[[row,col] | row <- range(rows), col <- range(columns), locs_adjacent(loc, [row,col])]
		",

		locs_adjacent: "def(Loc a, Loc b)
		  a[0] = b[0] and abs(a[1] - b[1]) = 1 or  //same row
		  abs(a[0] - b[0]) = 1  and (a[1] = b[1] or //one row apart
		        abs(a[1] - b[1]) = 1 and
				  ((a[0]%2 = 0 and a[1] = b[1]+1) or 
				   (a[0]%2 = 1 and a[1] = b[1]-1)))",

		all_locs: "[[row,col] | row <- range(rows), col <- range(columns)]",

		is_loc_on_board: "def(Loc loc) loc[0] >= 0 and loc[0] < rows and
		                           loc[1] >= 0 and loc[1] < columns and
								   (loc[1] >= lane.begin and loc[1] < lane.begin + lane.length
								   where lane = lanes[loc[0]])",
		is_loc_in_endzone: "def(Loc loc, int controller) loc[0] >= 0 and loc[0] < rows and
		                         (controller = 0 and loc[1] = lane.begin + lane.length or
								  controller = 1 and loc[1] = lane.begin - 1
								 where lane = lanes[loc[0]])",
								 
		construct_at_loc: "def(Loc loc) -> class creature|null
		   find(constructs, value.loc = loc)",
		creature_at_loc: "def(Loc loc) -> class creature|null
		   find(creatures, value.loc = loc)",

		creature_at_loc_or_die: "def(Loc loc) -> class creature
		   result asserting result | loc
		   where result = creature_at_loc(loc)",
		construct_at_loc_or_die: "def(Loc loc) -> class creature
		   result asserting result | loc
		   where result = construct_at_loc(loc)",

		creature_by_summon_id: "def(int id) -> class creature|null find(creatures, value.summon_id = id) or graveyard[id]",
		creature_by_summon_id_or_die: "def(int id) -> class creature result asserting result where result = find(creatures, value.summon_id = id) or graveyard[id]",
		permanent_by_summon_id: "def(int id) -> class creature|null find(permanents, value.summon_id = id) or graveyard[id]",
		permanent_by_summon_id_or_die: "def(int id) -> class creature result asserting result where result = find(permanents, value.summon_id = id) or graveyard[id]",

		//convenience function useful for when a card wants to scry for the
		//current player.
		scry: "def(int depth, {ability: class activated_ability|null, creature: class creature|null, card: class card|null} binding, class message.play_card_base info) ->commands
		if(info.choices,
		 [
		   set(me.player_obj.deck,
		       filter(me.player_obj.deck, index != info.choices[0])),
		   add(me.player_obj.hand,
		       [me.player_obj.deck[info.choices[0]]]),
		 ],
		 if(size(me.player_obj.deck) + size(me.player_obj.discard_pile) = 0, null,
		 [
		 	set(me.player_obj.deck, new_deck),
		 	set(me.player_obj.discard_pile, new_discard_pile),
			me.set_current_choice(binding + {
				info: info,
				text: 'Choose a card to put in your hand',
				options: map(new_deck[:min(size(new_deck), depth)],
				   construct('choice.card', {
						card: value,
						tag: index,
				   })),
			}),
		 ]
		where new_deck = if(size(me.player_obj.deck) < depth, me.player_obj.deck + shuffle(me.player_obj.discard_pile), me.player_obj.deck)
		where new_discard_pile = if(size(me.player_obj.deck) < depth, me.player_obj.discard_pile, [])
	  ))",

	},

	test: [
		{ command: "set(vars.game, construct('game', {tutorial: construct('tutorial'), players: [construct('player', {name: 'test', hand: []})]}))",
		  assert: "vars.game.rows = 5 and vars.game.columns = 5" }
	]
}
