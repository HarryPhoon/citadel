{
  bases: ["bot"],
  properties: {
    cards_to_consider: "def(game, player)
      player.hand
    ",

    score_summons: "def(game, card, loc)
      // cap glyphs with acolytes
      if(card.name = 'Acolyte', if(lane_has_neutral_glyphs(game, lane), 200, 100), 0) +

      // Put the hasty creatures in empty lanes
      if(card.name = 'Goblin' or card.name = 'Wolf Rider' or card.name = 'Goblin King', if(lane_empty(game, lane), 100, 0), 0) +

      // It's better to place Goblin Kings on middle lanes
      if(card.name = 'Goblin King', if(loc[0] <= 0 or loc[0] >= 5, -100, 0), 0) +

      // Don't neutralize your own glyphs
      if(card.name = 'Goblin' or card.name = 'Wolf Rider' or card.name = 'Goblin King', if(lane_has_my_glyphs(game, lane), -100, 100), 0) +

      // Emergency block with Acolytes, Goblins, and Sappers
      if(card.name = 'Acolyte' or card.name = 'Goblin' or card.name = 'Goblin Sapper', if(
        find(game.creatures,
          value.controller != game.nplayer and
          value.loc[0] = lane and
          value.tiles_from_start + value.move >= 6
        ) and (not find(game.creatures,
          value.controller = game.nplayer and
          value.loc[0] = lane and
          value.tiles_from_start = 1)
        ), 400, 0), 0) +

      // Only Goblin Sappers should dare to enter lanes that still have towers up
      if(lane_has_neutral_towers(game, lane) and card.name != 'Goblin Sapper', -1000, 200)
      where lane = loc[0]
    ",

    score_spell: "def(game, card, targets)
      // ALWAYS play free Nature's Blessings
      if(card.name = q(Nature's Blessing), if(game.players[game.nplayer].calculate_cost(card) <=1, 500, 50), 0) +

      // ALWAYS play free Endless Studies
      if(card.name = 'Endless Studies', if(game.players[game.nplayer].calculate_cost(card) = 0, 500, 50), 0) +

      // Fireballing enemies that are close to the end zone is a high priority
      if(card.name = 'Fireball', if(target_creature.controller != game.nplayer and target_creature.move + target_creature.tiles_from_start >= 8, 1000, 0), 0) +

      // If you have nothing else to do, upgrade your Acolytes and Goblins; TODO: upgrade as a response to removal
      if(card.name = 'Polymorph', if((not game.in_response_phase) and target_creature.controller = game.nplayer and game.current_player = game.nplayer and (target_creature.name = 'Goblin' or target_creature.name = 'Acolyte') and target_creature.tiles_from_start >= target_creature.move, 100, 0), 0) +

      // Use a surprise Quick Step to push one of your ublocked creatures into the end zone or right before end zone where the enemy can't summon a blocker
      if(card.name = 'Quick Step', if(
        target_creature.controller = game.nplayer and
        target_creature.move + target_creature.tiles_from_start >= 7 and
        target_creature.move + target_creature.tiles_from_start < 9 and
        (not find(game.creatures,
          value.controller != game.nplayer and
          value.loc[0] = targets[0][0] and
          value.tiles_from_start < 8 - target_creature.tiles_from_start)
      ), 400, 0), 0)

      where target_creature = game.creature_at_loc(targets[0])
    ",

    deck: "
      ['Acolyte']*3+
      ['Goblin']*3+
      ['Goblin King']*3+
      ['Goblin Sapper']*3+
      ['Wolf Rider']*3+
      [q(Nature's Blessing)]*3+
      ['Endless Studies']*3+
      ['Fireball']*3+
      ['Polymorph']*3+
      ['Quick Step']*3
    ",
  },
}
