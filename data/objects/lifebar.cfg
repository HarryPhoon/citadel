{
	id: "lifebar",
	animation: [
		{
			id: "normal",
			frames: 1,
			image: "life.png",
			rect: [0,0,7,7],
		}
	],

	properties: {
		destroy: "def() [destroy_children(), remove_object(me)]",
		destroy_children: "def() [remove_object(obj) | obj <- level.chars, obj.parent = me]",
		get_width: "def(life) min(12*life, 48)",
		creature_object: { get: "vars.creature_object",
		                   set: "[
						    destroy_children(),
						    set(x, new_x),
						    set(vars.creature_object, value),
							set(vars.target_life, max(0, value.life - value.wounds)),
							if((not vars.creature_object) or vars.creature_object.life != value.life, set(vars.current_life, value.life - value.wounds)),
						   ([
							set(me.vars.life_obj, new_life),
							set(new_life.parent, me),
							set(stretchable.parent, me),
							set(endcap.parent, me),
							map(notches, set(value.parent, me)),

							set(stretchable.zorder, zorder+2),
							set(endcap.zorder, zorder+3),
							set(new_life.zorder, zorder+4),
							map(notches, set(value.zorder, me.zorder+5)),

							set(life_pos, [2] + map(notches, value.x - (new_x+4)) + [endcap.x - (new_x+4)]),

							add_object(new_life),
							add_object(stretchable),
							add_object(endcap),
							map(notches, add_object(value)),

							set(stretchable.draw_area, [0, 0, get_width(value.life)/2 - 7, 7]),

							set(vars.life_obj, new_life)

							] where new_life = object('lifebar.life', new_x+6, mid_y, 1),
							        stretchable = object('lifebar.stretchable_bar', new_x+16, mid_y, 1),
									endcap = object('lifebar.right_endcap', new_x+3+get_width(value.life), mid_y, 1),

							        notches = map(range(value.life-1), object('lifebar.notch', new_x + 4 + ((value+1)*get_width(context.value.life))/context.value.life, mid_y, 1)))
						] where new_x = me.vars.xpos - (4 + get_width(value.life))/2" }
	},

	vars: {
		creature_object: null,
		current_life: null,
		target_life: null,
		life_obj: null,
		current_len: null,
		xpos: 0,
		life_pos: [],
	},

	timer_frequency: 3,

	on_process: "if(vars.life_obj and vars.creature_object.life != 0 and (vars.current_len = null or vars.current_len != target_len),
	    [
		   set(vars.life_obj.draw_area, [0,0, target_len, 3]),
		   set(vars.current_len, new_len)
		]
		 where new_len = if(vars.current_len = null, target_len,
		     if(vars.current_len < target_len,
			    vars.current_len+1,
				vars.current_len-1))
	     where target_len = vars.life_pos[vars.target_life]/2)",

	object_type: [
		{
			id: "notch",
			animation: [
				{
					id: "normal",
					image: "life.png",
					rect: [31,2,32,5],
				}
			],
		},
		{
			id: "life",
			animation: [
				{
					id: "normal",
					image: "lifebar.png",
					rect: [0,0,1,2],
				}
			],
		},

		{
			id: "stretchable_bar",
			animation: [
				{
					id: "normal",
					image: "lifebar.png",
					rect: [0,3,1,9],
				},
			],
		},

		{
			id: "right_endcap",
			animation: [
				{
					id: "normal",
					image: "life.png",
					rect: [28,0,30,7],
				},
			],
		},
	],
}
