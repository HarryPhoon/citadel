{
	id: "lifebar",
	animation: [
		{
			id: "normal",
			frames: 1,
			image: "life.png",
			rect: [0,0,7,7],
		}
	],

	properties: {
		get_wounds: "def(obj) if(vars.type = 'buildbar', obj.summoning_time - obj.summoning_counters, obj.wounds)",
		get_life: "def(obj) if(vars.type = 'buildbar', obj.summoning_time, obj.life)",
		destroy: "def() [destroy_children(), remove_object(me)]",
		destroy_children: "def() [remove_object(obj) | obj <- level.chars, obj.parent = me]",
		get_width: "def(life) min(12*life, 48)",
		creature_object: { get: "vars.creature_object",
		                   set: "
						   [
						    set(x, new_x),
							set(vars.creature_object, value),
						   
						   if((not vars.creature_object) or get_life(vars.creature_object) != get_life(value) or get_wounds(vars.creature_object) != get_wounds(value),
						   [
						    destroy_children(),
						    set(vars.creature_object, value),
							set(vars.target_life, new_target_life),
							if((not vars.creature_object) or get_life(vars.creature_object) != get_life(value), set(vars.current_life, get_life(value) - get_wounds(value))),
						   ([
							set(me.vars.life_obj, new_life),
							set(new_life.parent, me),
							set(stretchable.parent, me),
							set(endcap.parent, me),
							map(notches, set(value.parent, me)),

							set(stretchable.zorder, zorder+2),
							set(endcap.zorder, zorder+3),
							set(new_life.zorder, zorder+4),
							map(notches, set(value.zorder, me.zorder+5)),

							set(vars.life_pos, [2] + map(notches, value.x - (new_x+4)) + [endcap.x - (new_x+4)]),

							add_object(new_life),
							add_object(stretchable),
							add_object(endcap),
							map(notches, add_object(value)),

							set(stretchable.draw_area, [0, 0, get_width(get_life(value))/2 - 7, 7]),

							set(vars.life_obj, new_life)

							] where new_life = object('lifebar.life', new_x+6, mid_y, 1),
							        stretchable = object('lifebar.stretchable_bar', new_x+16, mid_y, 1),
									endcap = object('lifebar.right_endcap', new_x+3+get_width(get_life(value)), mid_y, 1),

							        notches = map(range(get_life(value)-1), object('lifebar.notch', new_x + 4 + ((value+1)*get_width(get_life(context.value)))/get_life(context.value), mid_y, 1)))
						]) 
						
						] where new_target_life = max(0, get_life(value) - get_wounds(value))
						  where new_x = me.vars.xpos - (4 + get_width(get_life(value)))/2" }
	},

	vars: {
		creature_object: null,
		current_life: null,
		target_life: null,
		life_obj: null,
		current_len: null,
		delay_move: 0,
		xpos: 0,
		life_pos: [],
		type: null
	},

	is_strict: true,

	on_draw: "if(vars.life_obj and get_life(vars.creature_object) != 0 and (vars.current_len = null or vars.current_len != target_len),
	    [
		   set(vars.life_obj.draw_area, [0,0, new_len, 3]),
		   set(vars.current_len, new_len),
		   if(vars.delay_move > 0, add(vars.delay_move, -1))
		]
		 where new_len = if(vars.current_len = null, target_len,
		   if(vars.delay_move > 0, vars.current_len,
			if(abs(vars.current_len - target_len) < 1.0, target_len,
		     if(vars.current_len < target_len,
			    vars.current_len+0.2,
				vars.current_len-0.2))))
		 )
	     where target_len = vars.life_pos[vars.target_life]/2",

	object_type: [
		{
			id: "notch",
			animation: [
				{
					id: "normal",
					image: "life.png",
					rect: [31,2,32,5],
				}
			],
		},
		{
			id: "life",
			animation: [
				{
					id: "normal",
					image: "lifebar.png",
					rect: [0,0,1,2],
				}
			],
		},

		{
			id: "stretchable_bar",
			animation: [
				{
					id: "normal",
					image: "lifebar.png",
					rect: [0,3,1,9],
				},
			],
		},

		{
			id: "right_endcap",
			animation: [
				{
					id: "normal",
					image: "life.png",
					rect: [28,0,30,7],
				},
			],
		},
	],
}
