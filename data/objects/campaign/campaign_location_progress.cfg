{
	id: "campaign_location_progress",
	is_strict: true,

	properties: {
		width: "int :: lib.gui.py(100)",
		height: "int :: lib.gui.py(30)",

		render: "def() ->commands
		set(animation, {
			id: 'fbo',
			image: 'fbo',
			x: 0, y: 0, w: width, h: height,
			scale: 1, frames: 1,
			fbo: c.render(width, height, [
				c.set_source_rgba(1,1,1,1),
				c.set_line_width(lib.gui.dpy(1.6)),
				c.move_to(lib.gui.py(4), lib.gui.py(12)),
				c.line_to(lib.gui.py(width - 4), lib.gui.py(12)),
				c.stroke(),

				c.set_source_rgba(0,1,1,1),
				c.move_to(lib.gui.py(4), lib.gui.py(12)),
				c.line_to(mix(lib.gui.py(4), lib.gui.py(width - 4), completion), lib.gui.py(12)),
				c.stroke(),

				c.translate(lib.gui.py(8), lib.gui.py(14)),
				map(fragments, [
					c.save(),
					c.set_source_rgba(1,1,1,1),
					value.path,
					c.fill(),
					c.restore(),
				])
			])
			where fragments = c.markup_text(sprintf(q(<font size='14'>%d%% Complete</font>), int(completion*100.0)), { width: lib.gui.py(width), scale: lib.gui.dpy(1.0) })
			where c = canvas()
		})
		",

		completion: { type: "decimal", change: "render()" },
	},

	events: {
		create: "render()",
	},
}
