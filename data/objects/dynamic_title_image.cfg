{
	is_strict: true,
	id: "dynamic_title_image",

	properties: {
		portrait: { type: "string" },
		portrait_path: "string :: 'images/backgrounds/' + portrait",

		animation_sequence: { type: "[CardAnim]" },

		calculate_sequence: "def([CardAnim] sequence) ->commands [
			set(num_cycles, sum(map(sequence, value.cycles)))
		]",

		calculate_anim: "def(int cycle_num) ->{zoom: decimal, focus: [decimal,decimal], cross_fade: {zoom: decimal, focus: [decimal,decimal], ratio: decimal}|null}
		{
			zoom: mix(anim.start_zoom, anim.end_zoom, ratio),
			focus: [
				mix(anim.start_focus[0], anim.end_focus[0], ratio), 
				mix(anim.start_focus[1], anim.end_focus[1], ratio), 
			],

			cross_fade: if(cycles_left < cross_fade_length,
			{
				zoom: fade.zoom,
				focus: fade.focus,
				ratio: decimal(cross_fade_length - cycles_left)/decimal(cross_fade_length)
			} where fade = calculate_anim(cycle_num + cycles_left))

		}
			where ratio = decimal(ncycle)/decimal(anim.cycles)
			where cycles_left = anim.cycles - ncycle
			where ncycle = if(nindex = 0, cycle, cycle - prev_cycles)
			where prev_cycles = sum(map(animation_sequence[:nindex], value.cycles))
		    where nindex = index(animation_sequence, anim)
			where anim = find_or_die(animation_sequence, cycle < sum(map(animation_sequence[:index+1], value.cycles)))
			where cycle = cycle_num%num_cycles
			where cross_fade_length = 12
		",

		num_cycles: { type: "int", default: 1 },

		render: "def(int ww, int hh, decimal scaling) ->map {
			id: 'fbo',
			image: 'fbo',
			x: 0, y: 0, w: ww, h: hh,
			scale: scaling,
			frames: 1,
			fbo: c.render(ww, hh, [
				c.scale(decimal(ww)/decimal(dim[0]),
				        decimal(hh)/decimal(dim[1])),
				c.paint_image(portrait_path),
			])
			where dim = c.image_dim(portrait_path)
			where c = canvas()
		}",

		target_size: { type: "[int,int]", default: [1644, 2048] },
		_rendered_size: { type: "null|[int,int]" },
	},

	on_check_needs_render: "if(_rendered_size != target_size, [
		set(animation, render(target_size[0], target_size[1], 0.5)),
		set(_rendered_size, target_size),
	])",

	on_create: "[
		calculate_sequence(animation_sequence),
		fire_event('check_needs_render'),
		set(uv_segments, [0,1]),
	]",

	shader: {
	name: "title_image_render",
	vertex: "
	uniform mat4 u_anura_mvp_matrix;
	uniform vec4 u_anura_sprite_area;
	uniform vec4 u_anura_draw_area;
	uniform vec4 u_screen_area;
	uniform float u_zoom;
	uniform vec2 u_target;
	uniform float u_alt_zoom;
	uniform vec2 u_alt_target;
	uniform float u_ratio;
	attribute vec4 a_anura_vertex;
	attribute vec2 a_anura_texcoord;
	varying vec2 v_texcoord;
	varying vec2 v_alt_texcoord;
	void main()
	{
		float xpos = (a_anura_texcoord[0] - u_anura_sprite_area[0]) / (u_anura_sprite_area[2] - u_anura_sprite_area[0]);
		float ypos = (a_anura_texcoord[1] - u_anura_sprite_area[1]) / (u_anura_sprite_area[3] - u_anura_sprite_area[1]);
		vec4 pos = vec4(u_screen_area[0]*(1.0-xpos) + u_screen_area[2]*xpos,
		                u_screen_area[1]*(1.0-ypos) + u_screen_area[3]*ypos, a_anura_vertex[2], a_anura_vertex[3]);

		gl_Position = u_anura_mvp_matrix * pos;

		vec2 target_pos = vec2(
		  u_anura_sprite_area[0] + u_target[0]*(u_anura_sprite_area[2] - u_anura_sprite_area[0]),
		  u_anura_sprite_area[1] + u_target[1]*(u_anura_sprite_area[3] - u_anura_sprite_area[1]));

		v_texcoord = mix(a_anura_texcoord, target_pos, u_zoom);

		if(u_ratio > 0.0) {
			vec2 alt_target_pos = vec2(
		  	  u_anura_sprite_area[0] + u_alt_target[0]*(u_anura_sprite_area[2] - u_anura_sprite_area[0]),
		  	  u_anura_sprite_area[1] + u_alt_target[1]*(u_anura_sprite_area[3] - u_anura_sprite_area[1]));
			v_alt_texcoord = mix(a_anura_texcoord, alt_target_pos, u_alt_zoom);
		}
	}
	",

	fragment: "
	uniform sampler2D u_anura_tex_map;
	uniform float u_ratio;
	uniform vec4 u_anura_sprite_area;
	uniform vec2 u_sprite_dim;
	varying vec2 v_texcoord;
	varying vec2 v_alt_texcoord;
	void main()
	{
		float px_width = (u_anura_sprite_area[2] - u_anura_sprite_area[0]) / u_sprite_dim[0];
		float px_height = (u_anura_sprite_area[3] - u_anura_sprite_area[1]) / u_sprite_dim[1];

		gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);

		for(float x = -1.0; x <= 1.0; x += 1.0) {
			for(float y = -1.0; y <= 1.0; y += 1.0) {
				float px = (px_width*x)/3.0;
				float py = (px_height*y)/3.0;

				gl_FragColor += texture2D(u_anura_tex_map, v_texcoord + vec2(px,py));
			}
		}

		gl_FragColor /= 9.0;

		if(u_ratio > 0.0) {
			gl_FragColor = mix(gl_FragColor, texture2D(u_anura_tex_map, v_alt_texcoord), u_ratio);
		}

		//gl_FragColor[3] = (v_texcoord[0] - u_anura_sprite_area[0])/(u_anura_sprite_area[2] - u_anura_sprite_area[0]);
	}
	",

	draw: "[
		set(uniform_commands.u_screen_area, level.dimensions),
		set(uniform_commands.u_sprite_dim, parent.target_size),
		set(uniform_commands.u_target, anim.focus),
		set(uniform_commands.u_zoom, anim.zoom),
		if(anim.cross_fade, [
			set(uniform_commands.u_alt_target, anim.cross_fade.focus),
			set(uniform_commands.u_alt_zoom, anim.cross_fade.zoom),
			set(uniform_commands.u_ratio, anim.cross_fade.ratio),
		], [
			set(uniform_commands.u_alt_target, [0,0]),
			set(uniform_commands.u_alt_zoom, 0),
			set(uniform_commands.u_ratio, 0),
		]),
	] where anim = parent.calculate_anim(level.cycle)
	",

	}
}
