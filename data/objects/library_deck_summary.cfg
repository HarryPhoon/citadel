{
	id: "library_deck_summary",
	is_strict: true,
	use_absolute_screen_coordinates: true,

	properties: {
		_font: { type: "string", init: "lib.font.regular_font" },
		_font_size: "int :: py(14)",

		py: "def(int pixels) ->int lib.citadel.py(pixels)",

		area: "{width: py(260), height: py(72)}",

		deck_name: { type: "string" },

		original_deck_name: { type: "null|string" },

		cards: "[string] ::
		if(_controller != null and _controller.editing_deck and _controller.deck,
		  fold(map(_controller.deck, [value.card.name]*value.count), a+b, []),
		  if(deck, deck.cards, [])
		  where deck = null|Deck :: _mm.account_info.decks[deck_name]
		)",

		avatar: "string ::
		if(deck != null and deck.avatar != null, deck.avatar,
		   deck != null and deck.cards != [], deck.cards[0],
		   'Acolyte')
		where deck = null|Deck :: _mm.account_info.decks[deck_name]
		",

		_last_cards: { type: "null|[string]" },

		_mm: { type: "obj matchmaking_base" },
		_controller: { type: "null|obj library_controller" },

		selected: {
			type: "bool",
			default: false,
			set: "[
				set(_data, value),
				bind_command(render),
			]",
		},

		highlighted: {
			type: "bool",
			default: false,
			set: "[
				set(_data, value),
				bind_command(render),
			]",
		},

		_cache: { type: "object", init: "create_cache(4)" },
		_invalidate: "def() ->commands set(_cache, create_cache(4))",

		_cache_key: "[selected, highlighted]",

		_card_avatar: { type: "obj card|null" },
		show_avatar: "def() ->commands
		if(_card_avatar and _card_avatar.card_type.name = avi, null,
		if(_controller != null,
		   spawn('card', 0, 0, {
			mid_x: x2,
			mid_y: mid_y,
			card_type: lib.citadel.create_card(avi),
			controller: _controller,
			card_size: 0.5,
			hud_alpha: 0.0,
			img_target: [lib.citadel.get_card_adjustment(avi, 'focus_x', 0.5), lib.citadel.get_card_adjustment(avi, 'focus_y', 0.5)],
			img_zoom: lib.citadel.get_card_adjustment(avi, 'focus_zoom', 0.3),
			show_shadow: false,
			use_absolute_screen_coordinates: true,
			zorder: zorder+1,
		   }, [
			remove_object(_card_avatar),
			set(_card_avatar, child),
		   ])
		)
		) where avi = avatar
		",

		render: "def(bool show_name=true) ->commands [
		if (area.width > 0 and area.height > 0, // needed to prevent crashes when alt tabbing
		set(animation, {
			id: 'fbo',
			image: 'fbo',
			x: 0, y: 0, w: area.width, h: area.height,
			scale: 1, frames: 1,
			fbo: query_cache(_cache, [selected,highlighted,show_name], c.render(area.width, area.height, [
				c.rectangle(0, area.height*0.1, area.width, area.height*0.8),
				c.set_source_rgba(0.06, 0.09, 0.13, 1.0),
				c.fill(),

				if(selected or highlighted,
						c.set_source_rgba(1.0, 1.0, 1.0, 1.0),
						c.set_source_rgba(0.29, 0.33, 0.36, 1.0)),
				c.rectangle(0, area.height*0.1, area.width, area.height*0.8),
				c.set_line_width(py(20)/10.0),
				c.stroke(),

				c.set_source_rgba(1, 1, 1, 1),

				c.save(),
				c.translate(py(4), py(30)),
				c.set_font(_font),
				c.set_font_size(py(22)),
				c.text_path_in_bounds(if(show_name, deck_name, ''), area.width, ['left']),
				c.fill(),
				c.restore(),
/*
				c.save(),
				c.translate(py(4), py(32)),
				c.set_font(_font),
				c.set_font_size(_font_size),
				c.text_path_in_bounds(str(size(deck)) + ' cards', area.width, ['left']),
				c.fill(),
				c.restore(),
*/
				[
				c.save(),
				c.translate(py(4), py(56)),
				c.set_font(_font),
				c.set_font_size(_font_size),
				c.text_path_in_bounds(txt, area.width, ['left']),
				c.fill(),
				c.restore(),
				]
				where txt = fold([
				  if(num_creatures, str(num_creatures) + ' creatures', ''),
				  if(num_buildings, str(num_buildings) + ' forts', ''),
				  if(num_spells, str(num_spells) + ' spells', ''),
				  if(num_lands, str(num_lands) + ' lands', ''),
				], if(a and b, a + ', ' + b, a + b))
				where num_creatures = count(deck_cards, value.creature_object and value.creature_object.is_construct = false and value.creature_object.is_building = false)
				where num_buildings = count(deck_cards, value.creature_object and value.creature_object.is_construct = false and value.creature_object.is_building)
				where num_spells = count(deck_cards, value.creature_object = null)
				where num_lands = count(deck_cards, value.creature_object and value.creature_object.is_construct)

			])),
		} where deck_cards = map(deck, lib.citadel.create_card(value))
		  where deck = cards
		  where c = canvas()
		  where entries = filter(level.chars, value is obj library_deck_entry))),
	set(_last_cards, cards),
	]",
	},

	on_process: "[
	if(_card_avatar, [
		set(_card_avatar.mid_x, x2),
		set(_card_avatar.mid_y, mid_y),
	]),
	if(_controller != null and _controller.editing_deck and cards != _last_cards, [
		fire_event('render')
	])
	]",

	on_click: "[
	  swallow_mouse_event(),

	  if(_controller = null, [
		[set(c.selected, false) | c <- level.chars, c is obj library_deck_summary],
		set(selected, true),
	  ]),

	  if(_controller != null and _controller.editing_deck = false,
	  _controller.deck_summary_clicked(me),
	  if(_controller != null and mouse_x < x + title_area.width+8 and mouse_y < y + title_area.height + 8,
	  [
		set_widgets([{
			type: 'text_editor',
			id: 'name_edit_box',
			font_size: 18,
			focus: true,
			width: 200,
			height: 30,
			text: deck_name,
			enabled: true,
			select_all: true,
			on_change_focus: q(fire_event('finish_editing_name')),
			on_enter: q(fire_event('finish_editing_name')),
		}]),
		render(false),
	  ]
	  )
	  where title_area = canvas().text_extents(_font, _font_size*2, deck_name)
	  where mouse_x = int <- head_or_die(level.player.ctrl_mice)[0]
	  where mouse_y = int <- head_or_die(level.player.ctrl_mice)[1]
	)
	]",

	on_mouse_enter: "[
		if(_controller != null and _controller.editing_deck = false, [
		   _controller.enter_deck_summary(me),
		   set(highlighted, true),
		]),

		if(_controller = null, set(highlighted, true)),
	]",

	on_mouse_leave: "[
		if(_controller != null and _controller.editing_deck = false, [
		   _controller.leave_deck_summary(me),
		   set(highlighted, false),
		]),

		if(_controller = null, set(highlighted, false)),
	]",

	on_finish_editing_name: "[
		if(new_name and regex_match(new_name, '.*[a-zA-Z].*'), [
			if(original_deck_name = null, set(original_deck_name, deck_name)),
			set(deck_name, new_name),
		]) where new_name = regex_replace(string<- (text_editor_widget<- widgets.name_edit_box).text, '[^0-9A-Za-z_-]', ''),
		set_widgets([]),
		bind_command(render),
	]",

	on_create: "[fire_event('render'), set(x2, x2), set(y, y)]",
	on_render: "[_invalidate(), show_avatar(), render()]",

	on_being_added: "if(_card_avatar != null, add_object(_card_avatar))",
	on_being_removed: "remove_object(_card_avatar)",

	on_type_updated: "fire_event('render')",
}
