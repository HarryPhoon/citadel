{
	id: "pause_dialog",

	is_strict: true,
	always_active: true,
	hidden_in_game: true,

	properties: {
		_mm: { type: "obj matchmaking_base" },
		_controller: { type: "obj citadel_controller" },
		_widgets: { type: "[custom_obj]", default: [] },
		_internal_client: { type: "object|null" },
		level_width: "int :: level.dimensions[2]",
		level_height: "int :: level.dimensions[3]",

		_menu_controller: { type: "null|obj game_menu_controller" },

		restart_game: "def() ->commands [
			if(_controller.game_setup_message != null, [
				debug('send restart game message'),
				set(_internal_client, cl),
				execute(me, tbs_send(cl, setup_message))
			] where cl = tbs_internal_client()
			
			  //map the session ID's to increase them all by 16.
			  //otherwise use the same setup message as used
			  //for this game.
			  where setup_message = _controller.game_setup_message + {users: map(([map]<-_controller.game_setup_message.users), value + {session_id: _controller.session_id + 16 + index})}
			),
		]",

		concede_game: "def() ->commands [
			_controller.concede_game(),
			exit_dialog(),
		]",

		exit_dialog: "def() ->commands [
		    set(_controller.in_background, false),
			remove_object(me),
		]",
	},

	on_create: "[
		set(_controller.in_background, true),

		spawn('game_menu_controller', {
			_exclude_from_pause: [me, _controller],

			buttons:

			[{
				text: 'Resume',
				callback: me.exit_dialog,
			}] +
			
			if(_controller.observer, [{
				text: 'Quit',
				callback: _controller.quit_game,
			}], 
			
			
		   if(_controller.game_setup_message != null and _controller.specified_deck = null, [
			{
				text: 'Quit',
				callback: _controller.quit_game,
			}, {
				text: 'Restart',
				callback: me.restart_game,
			}
			], []) +

			[{
				text: 'Concede',
				callback: _controller.concede_game,
			}]
			)
			
		}, [
			set(_menu_controller, child)
		]),

	]",

	on_process: "[
		if(_internal_client != null, tbs_process(_internal_client)),
//		_mm.process_matchmaking_connection(),
		_controller.process_connection(),
	]",

	on_settings_menu: "[
		debug('BBB: SETTINGS'),
		remove_object(_menu_controller),
		set(level.show_builtin_settings_dialog, false),
		exit_dialog(),
	]",

	on_message_received: "
	[
		switch(message.type,
		  'game_created',
		  [
		  teleport('level1.cfg', '', 'instant', playable)
		    where playable = object_playable('citadel_controller', 336, 372, 0, {
			_mm_client_state: _controller.get_mm_client_state(),
			game_server_address: '',
			game_server_port: 23456,
			game_setup_message: _controller.game_setup_message,
			session_id: _controller.session_id + 16,
			game_created: true,
			selected_deck: _controller.selected_deck,
			selected_deck_category: _controller.selected_deck_category,
			return_callback: _controller.return_callback,
			campaign_location: _controller.campaign_location,
			campaign_scenario: _controller.campaign_scenario,
			victory_unlock: _controller.victory_unlock,
		  })
		  ])
	]
			where message = map<- arg.message
	",

}
