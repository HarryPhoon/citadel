{
	id: "player_profile_pane",
	is_strict: true,

	properties: {
		title_controller: { type: "obj title_controller" },
		render: "def() ->commands [
		set(animation, lib.citadel.render_frame(c, ww, hh,
		[
			c.rectangle(0,0,ww,hh),
			c.set_source_rgba(0.09,0.10,0.11,1),
			c.fill(),

			c.set_line_width(lib.citadel.dpy(1.0)),
			c.rectangle(0,0,ww,hh),
			c.set_source_rgba(0.22,0.25,0.29,1),
			c.stroke(),

			c.move_to(0,lib.citadel.py(28)),
			c.line_to(ww,lib.citadel.py(28)),
			c.stroke(),

			c.set_source_rgba(1,1,1,1),

			map(fragments, [
				c.save(),
				c.translate(lib.citadel.py(8), lib.citadel.py(6)),
				value.path,
				c.fill(),
				c.restore(),
			])

			where fragments = c.markup_text(q(<font size='14'>Player Profile</font>), { width: lib.citadel.py(1000), scale: lib.citadel.dpy(1.0) }),

			map(fragments, [
				c.save(),
				c.translate(lib.citadel.py(164), lib.citadel.py(40)),
				value.path,
				c.fill(),
				c.restore(),
			])

			where fragments = c.markup_text(q(<font size='24'>) + lib.chat.nick_display(title_controller.username) + q(</font>), { width: lib.citadel.py(1000), scale: lib.citadel.dpy(1.0) }),



		]))
		where ww = lib.citadel.py(380)
		where hh = lib.citadel.py(184)
		where c = canvas()
		]",

		create_objects: "def() ->commands [
			remove_object(_avatar),
			spawn('player_avatar', {
				 player_num: 0,
				 _creature: null,
				 _avatar: title_controller.account_info.avatar,
				 zorder: zorder+10,
				 _top: y + lib.citadel.py(40),
				 _edge: lib.citadel.py(26),
				 _width: lib.citadel.py(130),
				 _height: lib.citadel.py(130),
				 _nick: '',
				 facing: 1,
				 highlight: true,
				 current_desaturation: 0.0,
				 target_desaturation: 0.0,
			}, [
				set(_avatar, child),
			]),
		]",

		_avatar: { type: "null|obj player_avatar" },
	},

	on_create: "render(); create_objects()",

	on_being_removed: "remove_object(_avatar)",

	on_type_updated: "fire_event('create')",
}
