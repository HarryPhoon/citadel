{
	id: "channeling_dialog",

	is_strict: true,
	hidden_in_game: true,

	zorder: 20000,
	always_active: true,

	properties: {
		_mm: { type: "obj matchmaking_base" },
		_controller: { type: "obj citadel_controller" },

		level_width: "int :: level.dimensions[2]",
		level_height: "int :: level.dimensions[3]",

		okay: "def() ->commands [
			_controller.set_amount_channeled(_current_channel);
			_on_complete,
			remove_object(me),
		]",
		cancel: "def() ->commands remove_object(me)",

		_on_complete: { type: "commands" },

		_adjust: "def(int nadjust) ->commands [
			if(new_value < 0, set(_current_channel, _max_channel),
			   new_value > _max_channel, set(_current_channel, 0),
			   set(_current_channel, new_value));

			update_label()
		] where new_value = _current_channel+nadjust",

		_max_channel: { type: "int" },
		_current_channel: { type: "int", default: 0 },

		update_label: "def() ->commands execute(me, [
			remove_object(_label),
			spawn('label', level_width/2 + lib.citadel.px(10), int(level_height*0.4), {
				zorder: zorder,
				_text: [str(_current_channel)],
			}, [
				set(_label, child)
			])
		])",

		_label: { type: "null|obj label" },

	},

on_being_removed: "[
	map(spawned_children, remove_object(value)),
	[set(c.paused, false) | c <- level.chars, c.paused],
	set(_controller.in_background, false),
]",

on_create: "[
	spawn('blur_controller', 0, 0, {
		begin_zorder: -1000000,
		end_zorder: 12000,
		blur_darken: 0.4,
	}, [
	]),

	set(_controller.in_background, true),
	[set(c.paused, true) | c <- level.chars, c != me, c != _controller],

	spawn('up_down_button', level_width/2 - lib.citadel.px(40), int(level_height*0.4) - lib.citadel.py(20), {
		_up: true,
		zorder: 15000,
		_onclick: bind(me._adjust, 1),
	}),

	spawn('up_down_button', level_width/2 - lib.citadel.px(40), int(level_height*0.4) + lib.citadel.py(20), {
		_up: false,
		zorder: 15000,
		_onclick: bind(me._adjust, -1),
	}),

	spawn('button_controller', int(level_width*0.4), int(level_height*0.6), {
		mid_x: level_width/2 - lib.citadel.px(180),
		text: 'Ok',
		on_click: me.okay,
		zorder: me.zorder,
		button_width: lib.citadel.px(160),
	}),

	spawn('button_controller', int(level_width*0.6), int(level_height*0.6), {
		mid_x: level_width/2 + lib.citadel.px(20),
		text: 'Cancel',
		on_click: me.cancel,
		zorder: me.zorder,
		button_width: lib.citadel.px(160),
	}),

	spawn('label', level_width/2, level_height/2, {
		zorder: zorder,
		_text: ['Choose how much mana to channel into this spell'],
	}),

	update_label(),
]"

}
