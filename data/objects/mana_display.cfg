{
id: "mana_display",
is_strict: true,

properties: {
	amount: { default: 0 },
	amount_delta: { default: 0 },
	amount_delta_str: "string :: if(amount_delta > 0, '+', '') + if(amount_delta = 0, '', str(amount_delta))",
	player_num: { type: "int" },
	title: { default: "MANA" },
	_render_key: "[amount]",

	_font_size: 12,
	_value_font_size: 18,

	_bg_color: "[0, 0.31, 0.32]",

	icon: { default: "mana.svg" },

	render: "def(int ww, int hh) ->commands [
	set(animation, {
		id: 'fbo',
		image: 'fbo',
		x: 0, y: 0, w: ww, h: hh,
		scale: 1, frames: 1,
		fbo: query_cache(global_cache(2), _render_key, c.render(ww, hh, [
			c.save(),

			c.scale(ww, hh),
			c.move_to(0.2, 0.5),
			c.line_to(0.2 + 0.25*0.8, 0),
			c.line_to(0.2 + 0.75*0.8, 0),
			c.line_to(1, 0.5),
			c.line_to(0.2 + 0.75*0.8, 1),
			c.line_to(0.2 + 0.25*0.8, 1),

			c.set_source_rgba(_bg_color[0], _bg_color[1], _bg_color[2], 1),
			c.fill(),

			c.restore(),

			c.save(),

			c.set_font(lib.citadel.sans_font),
			c.set_font_size(_font_size),
			c.translate(20, 36),
			c.rotate(-3.14*0.34),
			c.text_path(title), 
			c.set_source_rgba(1, 1, 1, 1),
			c.fill(),

			c.restore(),
	
			c.save(),

			c.translate(42, 12),
			c.scale(0.07),
			c.draw_svg('images/icons/' + icon),

			c.restore(),

			c.save(),
			c.set_font('Ubuntu-B.ttf'),
			c.set_font_size(_value_font_size),
			c.translate(0.6*ww - c.text_extents('Ubuntu-B.ttf', _value_font_size, str(amount)).width*0.5, 0.85*hh),
			c.text_path(str(amount)),
			c.set_source_rgba(1, 1, 1, 1),
			c.fill(),
			c.restore(),

			c.save(),
			c.set_font('Ubuntu-M.ttf'),
			c.set_font_size(_value_font_size-4),
			c.translate(0.6*ww + c.text_extents('Ubuntu-M.ttf', _value_font_size-4, amount_delta_str).width*0.5, 0.75*hh),
			c.text_path(amount_delta_str),
			c.set_source_rgba(1, 1, 1, 1),
			c.fill(),
			c.restore(),

		]) where c = canvas())
	})]",
},

on_update: "[render(100, 80)]",
}
