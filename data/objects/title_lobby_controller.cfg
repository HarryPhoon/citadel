{
	id: "title_lobby_controller",
	hidden_in_game: true,
	is_strict: true,

	properties: {
		level_width: "int :: level.dimensions[2]",
		level_height: "int :: level.dimensions[3] - 80",

		_mm_client_state: { type: "class mm_client_state" },

		_handle_logout: "def(obj combo_label label) ->commands [
			write_document('citadel_cookie', null),
			teleport('titlescreen.cfg', '', 'instant', object_playable('login_controller', 0, 0, 0)),
		]",

		create_objects: "def() ->commands [
			map(spawned_children, remove_object(value)),

			spawn('dynamic_title_image', level_width/2, level_height/2, {
				zorder: -1000,
				portrait: 'thespire.png',
				animation_sequence: [
					{start_focus: [0.5,0.75], end_focus: [0.5,0.75], start_zoom: 0.0, end_zoom: 0.5, cycles: 5000},
					{start_focus: [0.2,0.2], end_focus: [0.5,0.5], start_zoom: 0.0, end_zoom: 0.8, cycles: 5000},
				],
			}),

			spawn('label', level_width * 0.9, level_height*0.25, {
				_text: ['C I T A D E L'],
				_font_size: 96,
				_halign: 'right',
			}),

			spawn('label', level_width - 40, 40, {
				_text: ['Welcome, ' + _mm_client_state.username],
				_font_size: 16,
				_halign: 'right',
				_valign: 'top',
			}),

			spawn('combo_label', level_width - 52, 60, {
				_text: 'LOGOUT',
				_font_size: 14,
				_halign: 'right',
				grouping: 'logout',
				selected: false,
				index: 0,
				click_handler: me._handle_logout,
			}),
		]",
	},

	on_create: "[
		create_objects(),
	]",

	on_being_removed: "map(spawned_children, remove_object(value))",
 
 	on_type_updated: "fire_event('create')",

	on_window_resize: "[
		create_objects(),
	] where width = int :: arg.width, height = int :: arg.height",
}
