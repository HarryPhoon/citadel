{
	id: "tile_cursor",
	is_strict: true,

	properties: {
		tile: { type: "obj tile" },

		hex_height: { default: 100 },

		hex_size: "int :: int(hex_height*1.3)",

		hex_indent: "decimal :: (hex_size - hex_height)/decimal(hex_size)",
		hex_indent_inverse: "decimal :: 1.0 - hex_indent",

		border_size: "int :: 4",

		_mouseover: { default: false },

		render_cursor: "def(int w, int h) ->map {
			id: 'fbo',
			image: 'fbo',
			fbo: c.render(w + border_size*2, h + border_size*2, [
				c.translate(border_size, border_size),

				c.move_to(w*hex_indent, 0),
				c.line_to(w*hex_indent_inverse, 0),
				c.line_to(w, h*0.5),
				c.line_to(w*hex_indent_inverse, h),
				c.line_to(w*hex_indent, h),
				c.line_to(0, h*0.5),
				c.line_to(w*hex_indent, 0),
			] +

			if(tile.selectable,
			[
				c.set_line_width(if(_mouseover, 4, 1.5)),
				if(_mouseover, c.set_source_rgba(1, 1, 1, 1), c.set_source_rgba(1, 1, 1, 1)),
				c.stroke(),
			],
			[
				c.set_source_rgba(0, 0, 0, 0.5),
				c.fill(),
			])) where c = canvas(),

			x: 0, y: 0, w: w + border_size*2, h: h + border_size*2,
			scale: 1,
			frames: 1,
		}",
	},

	on_process: "if(_mouseover != tile.mouseover, [
		set(_mouseover, tile.mouseover),
		fire_event('render'),
	])",

	on_render: "[
		set(animation, render_cursor(hex_size, hex_height)),
	]",

	on_create: "[
		set(zorder, tile.zorder + 10),
		fire_event('render'),
		set(mid_x, tile.mid_x),
		set(mid_y, tile.mid_y),
		set(alpha, 0),
		animate(me, {
			alpha: 255
		}, {
			duration: 20,
		}),
	]",
}
