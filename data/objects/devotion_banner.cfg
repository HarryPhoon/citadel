{
id: "devotion_banner",
is_strict: true,
always_active: true,

properties: {
	_icon: { type: "obj devotion_icon", dynamic_initialization: true },
	school: { type: "int" },
	player_index: { type: "int" },
	_width: { type: "int" },
	_height: { type: "int" },
	_face_right: { default: true },

	_tokens: { type: "[obj devotion_token]", default: [] },

	active_tokens: {
		type: "int",
		default: 0,
		set: "[
			set(_data, value),
			map(_tokens, set(value.brightness, if(index < context.value, 255, 196))),
		]
		",
	},

	num_tokens: {
		type: "int",
		get: "int :: size(_tokens)",
		set: "if(val != size(_tokens), [
			if(val < size(_tokens), [
				map(_tokens[val:], remove_object(value)),
				set(_tokens, _tokens[:val]),
			],
			map(range(val - size(_tokens)),
			  spawn('devotion_token', {
				_width: _height/3,
				school: me.school,
				zorder: me.zorder+1,
				alpha: 0,
			  }, [
			  	add(me._tokens, [child]),
				animate(child, {
					alpha: 255,
				}, {
					duration: 20,
				})
			  ])
			)
		   ),

		   set(_last_pos, null),
		]) where val = min(value,10)
		",
		variable: false,
	},

	_last_pos: { type: "null|[int,int]" },

	render: "def() ->commands
	set(animation,
	lib.citadel.render_frame(c, _width, _height,
	[
		if(not _face_right, [
			c.translate(_width, 0),
			c.scale(-1, 1),
		]),
		c.set_source_rgba(1, 1, 1, 0.5),
		c.move_to(0, 0),
		c.line_to(_width - _height/2, 0),
		c.line_to(_width, _height/2),
		c.line_to(_width - _height/2, _height),
		c.line_to(0, _height),
		c.fill(),
	]
	)
	)
	where c = canvas()

	",
},

on_create: "[
	render(),
	spawn('devotion_icon', {
		_width: int(_height*1.2),
		mid_y: mid_y,
		mid_x: x + if(_face_right, 0, _width),
		_school: school,
		zorder: zorder+1,
	}, [
		set(_icon, child)
	])
]
",

on_process: "if([x,mid_y] != _last_pos, [
	set(_icon.mid_x, x + if(_face_right, 0, _width)),
	set(_icon.mid_y, mid_y),

	map(_tokens, [
		set(value.mid_y, mid_y + if(size(_tokens) <= 5, 0, if(index < 5, -1, 1)*(_width/16))),
		set(value.mid_x, x + if(_face_right, 0, _width) + (_width/4 + (_width/8)*(index%5))*if(_face_right, 1, -1)),
	]),
	
	set(_last_pos, [x,mid_y]),
])",

on_being_removed: "map(spawned_children, remove_object(value))",

}
