{
	id: "edit_deck_details_dialog",
	is_strict: true,
	always_active: true,
	prototype: ["dialog"],

	properties: {
		_controller: { type: "obj library_controller" },

		level_width: "int :: level.dimensions[2]",
		level_height: "int :: level.dimensions[3]",

		summary: { type: "obj library_deck_summary" },
		deck_name: { type: "string" },
		deck: { type: "Deck" },

		add_children: "def() ->commands [
			spawn('label', {
				x: lib.gui.py(12),
				y: lib.gui.py(12),
				_halign: 'left',
				_text: ['Deck Name:'],
				_font_size: lib.gui.py(32),
			}, [
				add_widget(child),
			]);

			spawn('text_entry', {
				x: lib.gui.py(24) + last_widget.img_w,
				y: lib.gui.py(12),
				_max_size: 16,
				_width: lib.gui.py(320),
				_height: lib.gui.py(42),
				font_size: lib.gui.py(32),
				default_text: 'Enter deck name',
				text: deck_name,
				_on_change: def()->commands me.update_avatar(),
			}, [
				add_widget(child),
				set(_name_entry, child),
			]);

			update_avatar();

			spawn('scrollable_pane', {
				x: lib.gui.py(24),
				y: lib.gui.py(252),
				area_width: lib.gui.py(800),
				area_height: lib.gui.py(400),
				use_absolute_screen_coordinates: true,

				elements: map(sorted_cards, {
					xpos: col_width*ncol,
					ypos: nrow*row_height,
					height: lib.gui.py(220),
					obj: object('deck_avatar', {
						_width: lib.gui.py(130),
						_portrait: avatar_card.portrait or 'acolyte.png',
						_name: if(_name_entry, _name_entry.text, deck_name),
						deck: deck,
						click_handler: def(obj deck_avatar avatar) ->commands me.select_avatar(avatar, value),
					})
					where avatar_card = lib.citadel.create_card(value)
				}
				
				where nrow = index/5
				where ncol = index%5)
				where sorted_cards = unique(sort(deck.cards))
			}, [
				add_widget(child),
			]) where col_width = lib.gui.py(150)
			   where row_height = lib.gui.py(200)
		]",

		close: "def() ->commands
			remove_object(me);
			if(_name_entry,
			   summary.change_deck_name(_name_entry.text)
			);

			fire_event(summary, 'render')
		",

		_name_entry: { type: "null|obj text_entry" },

		_avatar: { type: "null|obj deck_avatar" },
		update_avatar: "def() ->commands
		[
			remove_widget(_avatar),
			set(_avatar, null),

			if(summary.avatar,
				spawn('deck_avatar', {
					x: lib.gui.py(24),
					y: lib.gui.py(82),
					_width: lib.gui.py(130),
					_portrait: avatar_card.portrait or 'acolyte.png',
					_name: if(_name_entry, _name_entry.text, deck_name),
					deck: deck,
				}, [
					add_widget(child),
					set(_avatar, child),
				])
				where avatar_card = lib.citadel.create_card(summary.avatar)
			)
		]
		",

		select_avatar: "def(obj deck_avatar avatar, string portrait) ->commands
			set(summary.avatar_override, portrait);
			update_avatar()
		",

	},

	events: {

	},
}
