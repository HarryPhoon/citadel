{
	id: "multi_lobby_controller",
	is_human: true,
	hidden_in_game: true,
	is_strict: true,

	types: {
		UserEntry: "{user: string, status: string, user_label: obj label, status_label: obj label, server: GameServer|null, observe_button: null|obj button_controller, challenge_button: null|obj button_controller}",
	},

	properties: {
		title_controller: { type: "obj title_controller" },
		return_controller: { type: "custom_obj" },

		level_width: "int<- level.dimensions[2]",
		level_height: "int<- level.dimensions[3]",

		//the left/right side of the 'usable' area -- i.e. cutting off edges
		//to make for a 4:3 display.
		gui_left_edge: "int :: (level_width - (level_height*4)/3)/2",
		gui_right_edge: "int :: level_width - gui_left_edge",

		pane_left_edge: "int :: gui_right_edge - lib.citadel.py(445)",

		_chat_area: { type: "null|obj chat_area" },

		receive_chat_messages: "def([ChatMessage] messages) ->commands
		[
			if(_chat_area,
				map(messages, ; _chat_area.message_received(value))
			)
		]",

		_in_queue: { default: false },
		play_game: "def() ->commands execute(me, [
			title_controller.send_matchmaking_request({ type: 'pvp' }),
			set(_in_queue, true); _spawn_play_button()
		])",

		cancel_play_game: "def() ->commands execute(me, [
			title_controller.send_cancel_matchmaking_request(),
			set(_in_queue, false); _spawn_play_button()
		])",

		_play_button: { type: "null|obj button_controller" },
		_queued_label: { type: "null|obj label" },

		_spawn_play_button: "def() ->commands execute(me, [
			remove_object(_play_button),
			remove_object(_queued_label),
			set(_play_button, null),
			set(_queued_label, null),

			if(_in_queue, [
				spawn('label', 0, 0, {
					x: pane_left_edge + lib.citadel.py(12),
					y: lib.citadel.py(556),
					_text: ['Queued'],
					_halign: 'left',
					_font_size: lib.citadel.py(26),
					zorder: me.zorder+10,
				}, [
					set(_queued_label, child),
				]),
				spawn('button_controller', 0, 0, {
					x: pane_left_edge + lib.citadel.py(197),
					y: lib.citadel.py(536),
					text: 'cancel',
					_font_size: lib.citadel.py(32),
					button_width: (gui_right_edge - pane_left_edge)/2,
					button_height: lib.citadel.py(40),
					on_click: me.cancel_play_game,
					zorder: me.zorder+10,
				}, [
					set(_play_button, child),
				]),
			], [
				spawn('button_controller', 0, 0, {
					x: pane_left_edge,
					y: lib.citadel.py(560),
					text: 'play this deck',
					_font_size: lib.citadel.py(32),
					button_width: gui_right_edge - pane_left_edge,
					button_height: lib.citadel.py(44),
					on_click: me.play_game,
					zorder: me.zorder+10,
				}, [
					set(_play_button, child),
				]),
			]),
		])",

		_challenges: { type:"[{ user: string, obj: obj notification_toast}]", default: [] },

		update_challenges: "def() ->commands
		let starting_challenges = map(_challenges, value);
		set(lib.citadel.open_challenges, filter(lib.citadel.open_challenges, value.expiry_time > time().unix));

		map(filter(lib.citadel.open_challenges, find(_challenges, value.user = context.value.user) = null),
		  spawn('notification_toast', {
			_text: value.user + ' has challenged you to a game',
			_on_click: null,
			_options: [{
				text: 'Accept',
				on_click: bind(me.accept_challenge, value.user),
			}, {
				text: 'Decline',
				on_click: bind(me.decline_challenge, value.user),
			}],

			_duration: (value.expiry_time - time().unix)*60,
		  }, [
		  	add(_challenges, [{user: value.user, obj: child}])
		  ])
		);

		map(_challenges, if(find(lib.citadel.open_challenges, value.user = context.value.user) = null, remove_object(value.obj)));

		set(_challenges, filter(_challenges, find(lib.citadel.open_challenges, value.user = context.value.user) != null));

		schedule(60, bind_command(update_challenges))


		",

		accept_challenge: "def(string username) ->commands
		[
			title_controller.issue_challenge(username);
			set(lib.citadel.open_challenges, filter(lib.citadel.open_challenges, value.user != username));
			update_challenges()
		]
		",

		decline_challenge: "def(string username) ->commands
		[
			set(lib.citadel.open_challenges, filter(lib.citadel.open_challenges, value.user != username));
			update_challenges()
		]
		",



		create_objects: "def() ->commands execute(me, [
			update_challenges(),

			_spawn_play_button(),

			spawn('choose_deck_controller', 0, 0, {
				title_controller: title_controller,
				return_controller: return_controller,
				current_controller: me,
				_preview_x: pane_left_edge,
				_preview_y: lib.citadel.py(370),
			}),

			clear_chat_area(),

			set(draw_primitives, [
			{
				type: 'rect',
				color: [0.1, 0.1, 0.1, 1.0],
				area: [pane_left_edge, lib.citadel.py(30), gui_right_edge, lib.citadel.py(350)],
			},

			{
				type: 'rect',
				color: [0.14, 0.14, 0.14, 1.0],
				area: [pane_left_edge + lib.citadel.py(14), lib.citadel.py(130), gui_right_edge - lib.citadel.py(14), lib.citadel.py(350-14)],
			},

			{
				type: 'rect',
				color: [0.1, 0.1, 0.1, 1.0],
				area: [pane_left_edge, lib.citadel.py(365), gui_right_edge, lib.citadel.py(602)],
			},
			]),
		])",

		clear_chat_area: "def() ->commands
		[
			remove_object(_chat_area),
			spawn('chat_area', 0, 0, {
				x: pane_left_edge,
				y: lib.citadel.py(630),
				zorder: me.zorder+10,
				chat_width: gui_right_edge - pane_left_edge - lib.citadel.py(10),
				chat_height: lib.citadel.py(190),
				rect_color: [51,51,51,255],

				edit_bg_color: [26,26,26,255],
				edit_text_color: [128,128,128,255],
				edit_no_border: true,
				_message_send_handler: title_controller.send_global_chat,
			}, [
				set(me._chat_area, child),
			]);

			receive_chat_messages(lib.citadel.global_chat)
		]",

		_user_label: { type: "null|obj label" },
		_status_text_label: { type: "null|obj label" },
		_user_widget: { type: "null|obj scrollable_pane" },

		_user_list: {
			type: "[UserEntry]",
			default: [],
		},

		_calculate_servers: "def() ->{string -> GameServer}
		fold(
			map(title_controller.game_servers,
				fold(map(value.users, {(value): context.value}), a+b, {})
			),
		a+b, {}
		)
		",

		request_observe_game: "def(string username) ->commands
			title_controller.request_observe_game(username)
		",

		observe_game: "def(GameServer server, string|null username=null) ->commands
			title_controller.observe_game(server, username)
		",

		challenge: "def(UserEntry entry) ->commands
		[
			remove_object(entry.challenge_button),
			title_controller.issue_challenge(entry.user),
			debug(['CHALLENGE: ', entry.user]),
		]
		",

		_calculate_user_list: "def() ->commands
		(
			set(_user_list, filter(_user_list, find(users, value.id = context.value.user) != null));

			map(_user_list, value.status_label.set_text([new_status.status]) where new_status = find_or_die(users, value.id = context.value.user));

			add(_user_list,
				map(new_users, {
					user: value.id,
					status: value.status,
					user_label: object('label', 0, 0, {
						x: 0, y: 0,
						_valign: 'top',
						_halign: 'left',
						_text: [value.id],
						_font_size: lib.citadel.py(18),
						zorder: zorder+10,
					}),
					status_label: object('label', 0, 0, {
						x: 0, y: 0,
						_valign: 'top',
						_halign: 'left',
						_text: [value.status],
						_font_size: lib.citadel.py(18),
						zorder: zorder+10,
					}),
				})
			);

			map(_user_list, if(value.user != title_controller.username and (server != value.server or have_button != should_have_button), [
				set(value.server, server),
				remove_object(value.observe_button),
				set(value.observe_button,
				if(should_have_button,
					object('button_controller', {
						x: 0,
						y: 0,
						text: 'Observe',
						_font_size: lib.citadel.py(8),
						button_width: lib.citadel.py(40),
						button_height: lib.citadel.py(16),
						on_click: (def()->commands if(server, me.observe_game(server, value.user), me.request_observe_game(value.user))),
						zorder: me.zorder+10,
					})
				)
				),
			]) where have_button = (value.observe_button != null)
			   where should_have_button = (value.status = 'playing vs ai') or server != null
			   where server = server_map[value.user]);

			map(_user_list, if(value.user != title_controller.username and (server != value.server or not value.challenge_button), [
				set(value.server, server),
				remove_object(value.challenge_button),
				set(value.challenge_button,
				if(not server,
					object('button_controller', {
						x: 0,
						y: 0,
						text: 'Challenge',
						_font_size: lib.citadel.py(8),
						button_width: lib.citadel.py(40),
						button_height: lib.citadel.py(16),
						on_click: (def()->commands me.challenge(value)),
						zorder: me.zorder+10,
					})
				)
				),
			]) where server = server_map[value.user]);

			set(_user_list, sort(_user_list, a.user < b.user))

		)
		where new_users = filter(users, find(_user_list, value.user = context.value.id) = null)
		where users = title_controller.users_online
		where server_map = _calculate_servers()
		",

		_current_user_list_update: { type: "int", default: -1 },

		refresh_user_list: "def() ->commands
		if(_current_user_list_update != title_controller.users_online_update, [
			set(_current_user_list_update, title_controller.users_online_update),
			_calculate_user_list();
			[
				if(_user_widget = null,
				[
				   spawn('label', 0, 0, {
					x: pane_left_edge + lib.citadel.py(30),
					y: lib.citadel.py(128),
					_text: ['User'],
					_valign: 'top',
					_halign: 'left',
					_font_size: lib.citadel.py(18),
					_color: [0.8, 0.36, 0.36, 1.0],
					zorder: zorder+10,
				   }, [
				   	set(me._user_label, child),
				   ]),

				   spawn('label', 0, 0, {
					x: pane_left_edge + lib.citadel.py(180),
					y: lib.citadel.py(128),
					_valign: 'top',
					_halign: 'left',
					_text: ['Status'],
					_font_size: lib.citadel.py(18),
					_color: [0.8, 0.36, 0.36, 1.0],
					zorder: zorder+10,
				   }, [
				   	set(me._status_text_label, child),
				   ]),

				   spawn('scrollable_pane', 0, 0, {
					x: pane_left_edge + lib.citadel.py(30),
					y: lib.citadel.py(154),
					area_width: lib.citadel.py(400),
					area_height: lib.citadel.py(180),
					elements: entries,
					zorder: zorder+10,
				   }, [
				   	set(me._user_widget, child)
				   ])
				 ],

				   map(_user_widget.elements, remove_object(value.obj));
				   set(_user_widget.elements, entries); _user_widget.recalculate()
				)
			] where entries = map(_user_list, { obj: value.user_label, xpos: 0, ypos: lib.citadel.py(20)*index }) +
			                  map(_user_list, { obj: value.status_label, xpos: lib.citadel.py(150), ypos: lib.citadel.py(20)*index }) +
							  filter(map(_user_list, if(value.challenge_button, { obj: value.challenge_button, xpos: lib.citadel.py(290), ypos: lib.citadel.py(20)*index + lib.citadel.py(6) })), value != null) +
							  filter(map(_user_list, if(value.observe_button, { obj: value.observe_button, xpos: lib.citadel.py(360), ypos: lib.citadel.py(20)*index + lib.citadel.py(6) })), value != null)
		])
		",

		_status_label: { type: "null|obj text_area" },

		refresh_server_status_label: "def() ->commands
			if(text != _displayed_status, [
				set(_displayed_status, text),
				remove_object(_status_label),

				spawn('text_area', 0, 0, {
					x: pane_left_edge + lib.citadel.py(20),
					y: lib.citadel.py(38),
					_text: q(<font size='22'>) + text + q(</font>),
					text_width: 400,
					zorder: zorder+10,
					bg_color: [0.1, 0.1, 0.1, 1.0],
				}, [
					set(_status_label, child),
				]),
			])
			where text = _calculate_server_status_label()
		",

		_calculate_server_status_label: "def() ->string
			if(title_controller.num_users = null, q(Getting server status...),
				str(title_controller.num_users) + q( players online\n) +
				str(title_controller.num_queued) + q( players queued to play\n) +
				str(title_controller.num_games) + q( games being played)
			)
		",

		_displayed_status: { type: "null|string" },
	},

	on_process: "[
		refresh_server_status_label(),
		refresh_user_list(),
		if(_queued_label != null and cycle%10 = 0,
		   _queued_label.set_text(['Queued' + fold(map(range((cycle/10)%4), '.'), a+b, '')])
		)
	]",

	on_being_added: "
	[
		set(_user_label, null),
		set(_status_text_label, null),
		set(_user_widget, null),
		set(_user_list, []),
		set(_current_user_list_update, -1),
		set(_in_queue, false),
	];
		create_objects();
	[
		if(_user_widget != null and (_user_widget not in level.chars), add_object(_user_widget)),
		if(_user_label != null and (_user_label not in level.chars), add_object(_user_label)),
		if(_status_text_label != null and (_status_text_label not in level.chars), add_object(_status_text_label)),
		if(_status_label != null and (_status_label not in level.chars), add_object(_status_label)),
		if(_user_widget != null and (_user_widget not in level.chars), [add_object(_user_widget), _user_widget.recalculate()]),
	]",
	on_type_updated: "[map(spawned_children, remove_object(value)); fire_event('being_added')]",
}
