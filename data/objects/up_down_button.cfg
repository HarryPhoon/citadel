{
id: "up_down_button",
is_strict: true,

properties: {
	_up: { type: "bool" },

	_onclick: { type: "function() ->commands" },

	_mouseover: { default: false },

	render: "def(int ww, int hh) ->commands [
	set(animation, {
		id: 'fbo',
		image: 'fbo',
		x: 0, y: 0, w: ww, h: hh,
		scale: 1, frames: 1,
		fbo: query_cache(global_cache(4), [ww,hh,_up,_mouseover], c.render(ww, hh, [

			if(_up, [
				c.move_to(ww*0.5, 0),
				c.line_to(ww, hh),
				c.line_to(0, hh),
				c.line_to(ww*0.5, 0),
			], [
				c.move_to(ww*0.5, hh),
				c.line_to(ww, 0),
				c.line_to(0, 0),
				c.line_to(ww*0.5, hh),
			]),

			c.set_source_rgba(scheme[0]/255.0, scheme[1]/255.0, scheme[2]/255.0, 1.0),
			c.fill(),
		]) where scheme = [decimal]<- if(_mouseover, lib.citadel.button_color_scheme.focus, lib.citadel.button_color_scheme.normal)
		   where c = canvas()
		)
	}),
	]",
},

on_create: "[
	render(lib.citadel.px(32), lib.citadel.px(16)),
	set(mid_x, mid_x),
	set(mid_y, mid_y),
]",

on_mouse_enter: "[set(_mouseover, true), fire_event('create')]",
on_mouse_leave: "[set(_mouseover, false), fire_event('create')]",

on_click: "_onclick()",
}
