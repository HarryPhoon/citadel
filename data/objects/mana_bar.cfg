{
id: "mana_bar",
is_strict: true,

properties: {
	_prev_current_mana: { type: "int|null" },
	_prev_max_mana: { type: "int|null" },

	_animation_progress: { type: "decimal|null" },

	current_mana: { type: "int" },
	max_mana: { type: "int" },
	using_mana: { type: "int", default: 0, set: "if(value != _data, [set(_data, value), fire_event('create')])" },

	_font: { type: "string", init: "lib.font.regular_font" },
	_font_size: { default: 16 },

	_width: { type: "int" },
	_height: { type: "int" },

	px: "def(int pixels) ->int int(pixels*(_height/90.0))",

	render: "def(int ww, int hh) ->commands [
	set(animation, {
		id: 'fbo',
		image: 'fbo',
		x: 0, y: 0, w: ww+42, h: hh,
		scale: 1, frames: 1,
		fbo: c.render(ww+42, hh, [

			c.translate(0, hh - px(16)),

			map(range(if(_prev_max_mana is int and _animation_progress is decimal, _prev_max_mana, max_mana)),
				[
					c.save(),

					c.translate(col_pos*bar_width + (col_pos/(max_mana-1.0)) * 3.0, 0),


					[
						bar_outline,
						c.set_source_rgba(0.91, 0.86, 0.79, 1.0),
						c.set_line_width(1),
						c.stroke(),
					],

					if(value >= current_mana or (_animation_progress != null and _animation_progress <= 0.5) and value >= _prev_current_mana,
					[
					],
					value >= current_mana - using_mana,
					[
						bar_outline,
						c.set_source_rgba(0.91, 0.86, 0.79, 1.0),
						c.fill(),
						c.stroke(),
					],
					[
						bar_outline,
						c.set_source_rgba(0.53, 0.76, 0.07, if(_animation_progress is decimal and value >= _prev_current_mana, (_animation_progress - 0.5)*2.0, 1.0)),
						c.fill(),
						c.stroke(),
					]),
							
					c.restore(),
				] where row_pos = 0
				  where col_pos = value
			),

			c.translate(0, px(-5)),
			c.set_font(_font),
			c.set_font_size(px(_font_size)),
			c.set_source_rgba(1, 1, 1, 1),
			c.text_path('Mana'),
			c.fill(),

			c.translate(ww - c.text_extents(lib.font.regular_font, px(26), str(current_mana) + '/' + str(max_mana)).width - 4, 0),
			c.set_font(lib.font.regular_font),
			c.set_font_size(px(26)),
			c.text_path(str(current_mana) + '/' + str(max_mana)),
			c.fill(),
		])
		 where bar_outline = [
		 	c.rectangle(0, 0, bar_width - 4.0, px(14)),
		 ]
		 where bar_width = decimal(ww)/max_mana_interp
		 where max_mana_interp = if(max_progress = null,
		                            max_mana,
									max_mana*max_progress +
									(1.0 - max_progress)*_prev_max_mana asserting _prev_max_mana is int)
		 where max_progress = if(_animation_progress = null, null, min(1.0, _animation_progress*2.0))
		 where c = canvas()
	}),
	]",
},

on_create: "[
	if(_prev_max_mana is int, set(_animation_progress, 0.0)),

	fire_event('render'),
	set(x, x),
	set(y2, y2),
]",

on_render: "[
	render(_width, _height)
]",

on_process: "if(_animation_progress is decimal, [
	if(_animation_progress >= 1.0,
	[
	   set(_animation_progress, null),
	   set(_prev_max_mana, null),
	],
	   add(_animation_progress, 0.02)),

	fire_event('render'),
])",

on_type_updated: "fire_event('create')",


}
