{
	id: "disconnection_dialog",

	is_strict: true,
	always_active: true,
	hidden_in_game: true,

	properties: {
		_controller: { type: "obj citadel_controller" },

		level_width: "int :: level.dimensions[2]",
		level_height: "int :: level.dimensions[3]",

		_disconnected_label: { type: "obj label|null" },
		disconnected_players: { type: "{string -> int}", default: {} },

		disconnected_server: { default: false },

		px: "def(int pixels) ->int if(res%2 = 1, res+1, res) where res = int((decimal(pixels)*decimal(level_height))/900.0)",
	},

	on_process: "
	[
	if(find(level.chars, value = _controller) = null,
		remove_object(me)
	),

	if(disconnected_players or disconnected_server, [
		if(_disconnected_label = null or _disconnected_label.get_text() != text, [
			remove_object(_disconnected_label),
			spawn('label', level_width/2, level_height/2, {
				zorder: 19000,
				_text: text,
				_halign: 'center',
				_font_size: px(32),
				green: 0,
				blue: 0,
			}, [
				set(me._disconnected_label, child),
			]),
		])
	] where text = if(disconnected_server,
	                  ['Disconnected from server'],
	                  fold(map(disconnected_players,
	                   if(reconnection_time > seconds_disconnected,
	                      [key + ' has disconnected.',  'They have ' + str(reconnection_time - seconds_disconnected) + 's left to reconnect.'],
	                      [key + ' has been disconnected for ' + str(seconds_disconnected) + 's.'])
					   where seconds_disconnected = (level.cycle - value)/50), a+b)
				    )
	  where reconnection_time = 60)
	]
	",

	on_create: "[
		add(level.frame_buffer_shaders, [{
			label: 'disconnection_blur',
			begin_zorder: -10000000,
			end_zorder: 12000,
			shader_info: 'citadel_background_blur_hor',
		}]),
		add(level.frame_buffer_shaders, [{
			label: 'disconnection_blur',
			begin_zorder: -10000000,
			end_zorder: 12000,
			shader_info: 'citadel_background_blur_ver',
		}]),

		spawn('button_controller', level_width/2 - lib.citadel.px(120), level_height/2 + lib.citadel.py(140), {
			text: 'Quit',
			on_click: bind(_controller.return_callback,false,null),
			zorder: 20000,
			button_width: lib.citadel.px(240),
			button_height: lib.citadel.px(50),
		}),

	]",

	on_being_removed: " [
		remove_object(_disconnected_label),
		map(spawned_children, remove_object(value)),
		set(level.frame_buffer_shaders, filter(level.frame_buffer_shaders, value.label != 'disconnection_blur')),

	]
	",


}
