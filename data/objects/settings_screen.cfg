{
	id: "settings_screen",
	is_strict: true,

	zorder: 0,

	properties: {
		level_width: "int :: level.dimensions[2]",
		level_height: "int :: level.dimensions[3]",

		//the left/right side of the 'usable' area -- i.e. cutting off edges
		//to make for a 4:3 display.
		gui_left_edge: "int :: (level_width - (level_height*4)/3)/2",
		gui_right_edge: "int :: level_width - gui_left_edge",

		pane_a_left: "int :: gui_left_edge + lib.citadel.py(100)",
		pane_b_left: "int :: gui_right_edge - lib.citadel.py(100) - pane_width",
		pane_width: "int :: lib.citadel.py(340)",

		title_controller: { type: "obj title_controller" },

		close_settings: "def()->commands execute(me, [
			title_controller.transition_screen(
			; _preferences.save_preferences()
			; [
				remove_object(title_controller),
				add_object(title_controller),
				title_controller.show_ui(),
			],
			true)
		])",

		_video_settings_modified_label: { type: "null|obj label" },

		set_fullscreen: "def(bool value) ->commands [
			if(_video_settings_modified_label = null,
				spawn('label', 0, 0, {
					x: pane_a_left,
					y: lib.citadel.py(260),
					_color: [1,0,0,1],
					_halign: 'left',
					_text: ['Video mode settings will be applied',
					        'when you restart the game'],
					zorder: zorder+10,
					_font_size: lib.citadel.py(20),

				}, [
					set(_video_settings_modified_label, child)
				])
			),

			_preferences.set_preference_value('desktop_fullscreen', value),
		]",

		_avatar_pane: { type: "null|obj scrollable_pane" },

		_create_avatar: "def(string avatar) ->custom_obj
			object('player_avatar', 0, 0, {
				player_num: 0,
				_creature: null,
				_avatar: avatar,
				_width: int(pane_width*0.8),
				_height: int(pane_width*0.8),
				_nick: '',
				zorder: zorder+15,
				facing: 1,
				highlight: true,
				current_desaturation: 0.0,
				target_desaturation: 0.0,
				click_handler: (def(obj player_avatar a) ->commands [set_avatar(avatar)]),
			})
		",

		set_avatar: "def(string avatar) ->commands
		[
			[[set(c.paused, false)] | c <- level.chars, c.paused],
			remove_object(_blur),
			set(_blur,null),
			remove_object(_avatar_pane),
			set(_avatar_pane, null),
			title_controller.set_avatar(avatar),
		]
		",

		_blur: { type: "null|obj blur_controller" },

		_change_password_button: { type: "null|obj button_controller" },
		_change_password_label: { type: "null|obj label" },
		password_changed: "def() ->commands
		if(_passwords_match,
		   if(_change_password_button = null,

			[
			remove_object(_change_password_label),
			set(_change_password_label, null),

			spawn('button_controller', 0, 0, {
				x: pane_b_left + lib.citadel.py(56),
				y: lib.citadel.py(700),
				button_width: pane_width/2,
				text: 'Change Password',
				_font_size: lib.citadel.py(14),
				on_click: me.change_password,
				zorder: zorder+20,
			}, [
				set(me._change_password_button, child),
			]),
			]
		   ),

		   if(_change_password_label = null and _passwd_entry != null and _confirm_passwd_entry != null and (_passwd_entry.has_focus or _confirm_passwd_entry.has_focus),
		   [
		   		remove_object(_change_password_button),
		   		set(me._change_password_button, null),

				spawn('label', 0, 0, {
					x: pane_b_left + lib.citadel.py(50),
					y: lib.citadel.py(700),
					_font_size: lib.citadel.py(20),
					_halign: 'left',
					_text: ['Passwords do not match'],
					_color: [1,0,0,1],
					zorder: zorder+10,
				}, [
					set(me._change_password_label, child)
				]),
		   ]
		   )
		)
		",

		_passwords_match: "bool ::
			_passwd_entry != null and _confirm_passwd_entry != null and _passwd_entry.text != '' and _passwd_entry.text = _confirm_passwd_entry.text
		",

		change_password: "def() ->commands
		if(new_passwd and _passwords_match, title_controller.reset_passwd(new_passwd))
			where new_passwd = if(_passwd_entry and _passwd_entry.text != 'Change Password', _passwd_entry.text)
		",

		_passwd_entry: { type: "null|obj text_entry" },
		_confirm_passwd_entry: { type: "null|obj text_entry" },

		change_avatar: "def() ->commands
		if(_avatar_pane != null,
		[
			[[set(c.paused, false)] | c <- level.chars, c.paused],
			remove_object(_blur),
			set(_blur,null),
			remove_object(_avatar_pane),
			set(_avatar_pane, null),
		],
		[
			[[set(c.paused,true)] | c <- level.chars, c not in [me,title_controller]],
			remove_object(_blur),
			spawn('blur_controller', 0, 0, {
				begin_zorder: me.zorder-100,
				end_zorder: me.zorder+15,
			}, [
				set(_blur,child)
			]),

			spawn('scrollable_pane', 0, 0, {
				x: pane_b_left - pane_width*2,
				y: lib.citadel.py(20),
				area_width: pane_width*2,
				area_height: lib.citadel.py(800),
				zorder: me.zorder+100,
				focus_override: true,

				elements: [],
				pending_elements: map(lib.citadel.basic_avatars,
					{
						create: bind(me._create_avatar, value),
						xpos: pane_width*(index%2),
						ypos: int(lib.hex.height_from_width(pane_width))*(index/2),
						height: int(lib.hex.height_from_width(pane_width)),
					}
				),
			}, [
				set(me._avatar_pane, child),
			]);

			if(_avatar_pane, _avatar_pane.recalculate())
		])",

		_displayed_avatar: { type: "null|string" },
		_avatar: { type: "null|obj player_avatar" },
		_show_avatar: "def() ->commands [
			set(_displayed_avatar, title_controller.account_info.avatar),
			remove_object(_avatar),
			spawn('player_avatar', 0, 0, {
				player_num: 0,
				_creature: null,
				_avatar: title_controller.account_info.avatar,
				zorder: zorder+15,
				_top: lib.citadel.py(200),
				_width: int(pane_width*0.8),
				_height: int(pane_width*0.8),
				_edge: pane_b_left,
				_nick: '',
				facing: 1,
				highlight: true,
				current_desaturation: 0.0,
				target_desaturation: 0.0,
			}, [
				set(_avatar, child),
			]),
		]",

		show_dialog: "def() ->commands [
			set(draw_primitives, [
			{
				type: 'rect',
				area: [pane_a_left, lib.citadel.py(110), pane_a_left+pane_width, lib.citadel.py(150)],
				color: [128,128,128,255],
			},
			{
				type: 'rect',
				area: [pane_b_left, lib.citadel.py(110), pane_b_left+pane_width, lib.citadel.py(150)],
				color: [128,128,128,255],
			},
			{
				type: 'rect',
				area: [pane_a_left, lib.citadel.py(390), pane_a_left+pane_width, lib.citadel.py(430)],
				color: [128,128,128,255],
			},
			]),

			spawn('label', 0, 0, {
				mid_x: level_width/2,
				y: lib.citadel.py(30),
				_font_size: lib.citadel.py(48),
				_halign: 'center',
				_text: ['Settings'],
				zorder: zorder+10,
			}),

			spawn('label', 0, 0, {
				mid_x: pane_a_left + pane_width/2,
				mid_y: lib.citadel.py(138),
				_font_size: lib.citadel.py(32),
				_halign: 'center',
				_text: ['Video'],
				zorder: zorder+10,
			}),

			spawn('label', 0, 0, {
				mid_x: pane_b_left + pane_width/2,
				mid_y: lib.citadel.py(138),
				_font_size: lib.citadel.py(32),
				_halign: 'center',
				_text: ['Account'],
				zorder: zorder+10,
			}),

			spawn('label', 0, 0, {
				mid_x: pane_a_left + pane_width/2,
				mid_y: lib.citadel.py(418),
				_font_size: lib.citadel.py(32),
				_halign: 'center',
				_text: ['Sounds'],
				zorder: zorder+10,
			}),

			_show_avatar(),

			spawn('button_controller', 0, 0, {
				x: pane_b_left + lib.citadel.py(56),
				y: lib.citadel.py(480),
				button_width: pane_width/2,
				text: 'Change Avatar',
				on_click: me.change_avatar,
				zorder: zorder+20,
			}, [
			]),

			spawn('text_entry', {
				x: pane_b_left + lib.citadel.py(56),
				y: lib.citadel.py(620),
				text_width: int(pane_width*0.6),
				zorder: zorder+20,
				text: 'New Password',
				password: true,
				clear_on_focus: true,
				_on_enter: me.change_password,
				_on_tab: (def() ->commands if(me._confirm_passwd_entry and me._passwd_entry, [set(me._confirm_passwd_entry.has_focus, true), set(me._passwd_entry.has_focus, false)])),
				_on_change: me.password_changed,
			}, [
				set(me._passwd_entry, child),
			]),

			spawn('text_entry', {
				x: pane_b_left + lib.citadel.py(56),
				y: lib.citadel.py(660),
				text_width: int(pane_width*0.6),
				zorder: zorder+20,
				text: 'Confirm Password',
				password: true,
				clear_on_focus: true,
				_on_enter: me.change_password,
				_on_tab: (def() ->commands if(me._confirm_passwd_entry and me._passwd_entry, [set(me._confirm_passwd_entry.has_focus, false), set(me._passwd_entry.has_focus, true)])),
				_on_change: me.password_changed,
			}, [
				set(me._confirm_passwd_entry, child),
			]),
			
			spawn('checkbox_controller', 0, 0, {
				text: 'Fullscreen',
				x: pane_a_left,
				y: lib.citadel.py(220),
				checked: _preferences.get_bool_preference_value('desktop_fullscreen'),
				on_update: me.set_fullscreen,
				zorder: zorder+10,
			}, [
			]),

			spawn('button_controller', 0, 0, {
				x2: gui_right_edge - lib.citadel.py(280),
				y2: level_height - lib.citadel.py(32),
				on_click: me.close_settings,
				text: 'Close',
				zorder: zorder+10,
			}, [
			]),

			spawn('slider_widget', 0, 0, {
				x: pane_a_left,
				y: lib.citadel.py(500),
				slider_width: pane_width,
				slider_height: lib.citadel.py(20),
				slider_value: _preferences.sound_volume,
				zorder: me.zorder+10,
				value_change_handler: me._change_sound_volume,
			}, [
				set(me._sound_slider, child),
			]),

			spawn('slider_widget', 0, 0, {
				x: pane_a_left,
				y: lib.citadel.py(620),
				slider_width: pane_width,
				slider_height: lib.citadel.py(20),
				slider_value: _preferences.music_volume,
				zorder: me.zorder+10,
				value_change_handler: me._change_music_volume,
			}, [
				set(me._music_slider, child),
			]),

			_change_music_volume(_preferences.music_volume),
			_change_sound_volume(_preferences.sound_volume),
		]",

		_preferences: "game_preferences :: game_preferences()",

		_change_sound_volume: "def(decimal value) ->commands
		[
			remove_object(_sound_label),
			set(_preferences.sound_volume, value),
			spawn('label', 0, 0, {
				x: pane_a_left,
				y: lib.citadel.py(460),
				_halign: 'left',
				_text: ['Sound Effects: ' + str(int(100*value))],
				zorder: zorder+10,
				_font_size: lib.citadel.py(20),
			}, [
				set(me._sound_label, child),
			]),
		]
		",

		_change_music_volume: "def(decimal value) ->commands
		[
			remove_object(_music_label),
			set(_preferences.music_volume, value),
			spawn('label', 0, 0, {
				x: pane_a_left,
				y: lib.citadel.py(580),
				_halign: 'left',
				_text: ['Music: ' + str(int(100*value))],
				zorder: zorder+10,
				_font_size: lib.citadel.py(20),
			}, [
				set(me._music_label, child),
			]),
		]
		",

		_sound_slider: { type: "null|obj slider_widget" },
		_music_slider: { type: "null|obj slider_widget" },

		_sound_label: { type: "null|obj label" },
		_music_label: { type: "null|obj label" },
	},

	on_process: "
	if(_displayed_avatar != title_controller.account_info.avatar,
	   _show_avatar())
	",

	on_create: "[
		set(alpha,0),
		add(level.frame_buffer_shaders, [{
			begin_zorder: -10000,
			end_zorder: zorder+1,
			shader_info: 'citadel_settings_background',
		}]),

		show_dialog(),
	]",

	on_being_removed: "[
		set(level.frame_buffer_shaders, filter(level.frame_buffer_shaders, value.shader_info != 'citadel_settings_background')),
		map(spawned_children, remove_object(value)),
	]",

	on_type_updated: "[
		fire_event('being_removed'),
		fire_event('create'),
	]",
}
