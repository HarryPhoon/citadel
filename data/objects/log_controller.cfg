{
id: "log_controller",
is_strict: true,

next_animation: "'normal'",
always_active: true,
hidden_in_game: true,

types: {
	LogEntry: "{
		state: class game,
		card: class card_base,
		player_index: int,
	}",
},

properties: {
	controller: { type: "obj citadel_controller" },

	_pane: { type: "null|obj scrollable_pane" },
	pane: "obj scrollable_pane<-  _pane",

	screen_width: "int<- level.dimensions[2]",
	screen_height: "int<- level.dimensions[3]",

	history: { type: "[LogEntry]", default: [] },

	process_game_state: "def(class game state) ->commands execute(me,
	if(state.stack != [] and state.stack[size(state.stack)-1].card != null,
		if(find(history, value.card = card) = null,
			add_entry({ state: get_modified_object(state, def(class game game) ->commands null), card: card, player_index: entry.player_index })
		)

	where card = class card<- entry.card
	where entry = state.stack[size(state.stack)-1])
	)
	",

	add_entry: "def(LogEntry entry) ->commands [
		set(card.mouse_enter_handler, def()->commands mouse_enter(card)),
		set(card.mouse_leave_handler, def()->commands mouse_leave(card)),
		add(me.history, [entry]),
		let old_elements = map(pane.elements, {obj: value.obj, ypos: value.ypos});
		let old_position = pane.current_position;
		add(pane.elements, [{
			xpos: lib.citadel.px(if(entry.player_index%2 = 0, 10, 60)),
			ypos: if(pane.elements, back_or_die(pane.elements).ypos + lib.citadel.px(if(is_response, 30, 60)), 0),
			obj: card
		}]); pane.force_to_bottom(); pane.recalculate(); pane.animate_element_movement_from_previous_position(old_elements, 20, old_position)

	] where card = object('card', 0, 0, {
		card_type: lib.citadel.create_card(entry.card.name),
		card_size: 0.2,
		controller: controller,
		show_shadow: false,
	})
	  where is_response = if(history, find(entry.state.stack, value.card != null and last.card != null and value.card._uuid = last.card._uuid) != null where last = history[size(history)-1], false)",

	mouse_enter: "def(obj card card) ->commands [
		set(card.brightness, 512),
		set(_preview_card, object('card', screen_width/2, screen_height/2, { zorder: 10000, card_type: card.card_type, card_size: 2.0, controller: controller }))
	]",

	mouse_leave: "def(obj card card) ->commands [
		set(card.brightness, 255),
		if(_preview_card != null and _preview_card.card_type = card.card_type, set(_preview_card, null))
	]
	",

	_preview_card: { type: "obj card|null", set: "[remove_object(_data), if(value, add_object(value)), set(_data, value)]" },
},

on_create: "
	spawn('scrollable_pane', 0, 0, {
		x: screen_width - controller.avatar_width- lib.citadel.px(136),
		y: lib.citadel.px(120),
		area_width: lib.citadel.px(130),
		area_height: lib.citadel.px(320),
		elements: [],
		zorder: lib.citadel.zorder('game_log'),
		hide_scrollbar_on_mouse_off: true,
	}, [
		set(me._pane, child),
	])",

on_type_updated: "
	debug('update log controller');
	let hist = history;
	set(history, []);
	map(pane.elements, remove_object(value.obj));
	set(pane.elements, []);
	map(hist, ; process_game_state(value.state))
",
}
