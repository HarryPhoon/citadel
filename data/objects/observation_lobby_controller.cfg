{
id: "observation_lobby_controller",
next_animation: "'normal'",
is_human: true,
is_strict: true,
always_active: true,
hidden_in_game: true,


properties: {
	client: { type: "object" },
	_messages_in_flight: 0,

	_time_since_last_request: 0,
},

on_create: "[
	tbs_send(client, { type: 'query_status', user: USERNAME }),
	add(_messages_in_flight, 1),
	set_widgets([{
		type: 'label',
		text: 'Waiting for server...',
		id: 'status_label',
		x: level.dimensions[2]/2,
		y: level.dimensions[3]/2,
		size: 48,
		color: 'white'
	}])
]",

on_process: "[
	if(_messages_in_flight = 0,
		if(_time_since_last_request > 250,
		[
			tbs_send(client, { type: 'query_status', user: USERNAME }),
			set(_time_since_last_request, 0),
			add(_messages_in_flight, 1),
		],
		[
			add(_time_since_last_request, 1)
		])
	),

	tbs_process(client),
]",

on_connection_error: "[
	//TODO: currently we die die die on an error, handle this more nicely.
	debug('connection error') asserting cycle = -1
]",

on_message_received: "[
	add(_messages_in_flight, -1),
	switch(message.type,
	  'server_status', [
	  	set_widgets([{
			type: 'grid',
			columns: 1,
			column_alignments: 'center',
			x: level.dimensions[2]/2,
			y: level.dimensions[3]/2,
			children: map([map]<- message.servers,
			{
				type: 'label',
				text: description,
				size: 14,
				color: 'white',
			}
				
				where description = (string<- users[0].user) + ' vs ' + (string<- users[1].user)
				asserting size(users) >= 2
				where users = [map]<- value.users
			) + if(message.servers = [], [{
				type: 'label',
				text: 'No games currently in progress',
				size: 14,
				color: 'white',
			}], [])
		}])
	  ],

	  [
	  	debug('unknown message: ' + message.type),
	  ])
] where message = map<- arg.message",

}
