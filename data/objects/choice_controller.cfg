{
id: "choice_controller",
is_human: false,
is_strict: true,
hidden_in_game: true,
always_active: true,
zorder: 20000,

properties: {
	_mm: { type: "obj matchmaking_base" },
	_controller: { type: "obj citadel_controller" },
	_label: { type: "null|obj label" },

	choices_made: {
		type: "[{
			choice: int|string|null,
			index: int,
		}]",

		default: [],
	},

	creature_options: { type: "[obj creature]", default: [] },
	original_cards: { type: "[{card: obj card, in_opponent_hand: bool } ]", default: [] },

	level_width: "int :: level.dimensions[2]",
	level_height: "int :: level.dimensions[3]",

	exit_dialog: "def() ->commands [
		[c.hide_halo() | c <- level.chars, c is obj card, c.paused = false],

		map(children, value.start_fizzle_animation()),

		set(_controller.in_background, false),
		map(original_cards, [
			set(value.card.in_opponent_hand, value.in_opponent_hand),
			set(value.card.is_choice, false),
		]),
		map(level.chars, if(value.paused or (value in all_chat_obj) or (value in creature_options) or value = _controller or find(original_cards, value.card = context.value), set(value.paused, false), remove_object(value)))
	      where all_chat_obj = get_all_chat_objects(),

		remove_object(_blur_controller),
		set(_blur_controller, null),

		remove_object(_label),
		set(_label, null),

	]",


	children: { type: "[obj card]", default: [] },

	game: { type: "class game_state" },

	_blur_controller: { type: "null|obj blur_controller" },

	process_presence: "def(any info) ->commands [
		[if(c.card_type._uuid = info, c.show_halo(), c.hide_halo()) | c <- level.chars, c is obj card, c.paused = false]
	]",

	_spawned_descendants: "def(custom_obj o) ->[custom_obj]
		[o] + fold(map(o.spawned_children, _spawned_descendants(value)), a+b, [])
	",

	get_all_chat_objects: "def() ->[custom_obj]
	if(chat_widget, _spawned_descendants(chat_widget), [])
	  where chat_widget = find(level.chars, value is obj chat_widget),
		
	",
},

on_create: "[
	spawn('blur_controller', {}, [set(me._blur_controller, child)]),

	set(_controller.in_background, true),
	[set(c.paused, true) | c <- level.chars, c != me, c != _controller, c not in all_chat_obj]
	  where all_chat_obj = get_all_chat_objects(),

	[[add(creature_options, [c]), set(c.paused, false), add(c.zorder, 10000), set(c.option_index, index(game.current_choice.options, c.creature_object.loc))] | c <- level.chars, c is obj creature, c.creature_object.loc in game.current_choice.options],

			set(zorder, 20000),
			execute(me, map(range(24), schedule(value, set(alpha, value*8)))),

			map(cards,

			if(card_obj,

			[
			add(original_cards, [{card: card_obj, in_opponent_hand: card_obj.in_opponent_hand}]),

			set(card_obj.paused, false),
			set(card_obj.in_opponent_hand, false),
			set(card_obj.choice_value, int<- value.tag),
			set(card_obj.choice_index, if(value in game.current_choice.card_options, index(game.current_choice.card_options, value), int<- value.tag)),

			set(card_obj.zorder, 20000+index),

			animate(card_obj, {
				mid_x: xpos,
				mid_y: ypos,
				rotate: 0,
				card_size: 2,
			}, {
				duration: 40 + index*10,
				on_complete: [
					set(card_obj.is_choice, _controller.myturn),
					if(_controller.myturn, bind_command(card_obj.flip_card_over, false, 20)),
				]
			}),

			],
			
			spawn('card', 0, level_height, {
				card_type: if(value.card is string, lib.citadel.create_card(value.card), value.card),
				controller: _controller,
				card_size: 2.0,
				show_back: not _controller.is_replay,
			  }, [
				log('spawn card ' + index + '/' + size(cards)),
				set(child.is_choice, _controller.myturn),
				set(child.choice_value, int<- value.tag),
				set(child.choice_index, if(value in game.current_choice.card_options, index(game.current_choice.card_options, value), int<- value.tag)),
				set(child.zorder, 20000),
				set(child.rotate, 180),
				add(children, [child]),
				child.set_show_back(not _controller.is_replay),
				set(child.scale, 0.1),
				animate(child, {
					scale: 1,
					},{
					duration: 20,
					name: 'card_nearing',
				}),

				animate(child, {
					mid_x: xpos,
					mid_y: ypos,
					rotate: 0,
				}, {
					duration: 40 + index*10,
					on_complete: [
						set(child.is_choice, _controller.myturn),
						if(_controller.myturn, bind_command(child.flip_card_over, false, 20)),
					]
				}),

				add(me.original_cards, [{card: child, in_opponent_hand: false}]),

			])
			)
			where xpos = 180 + ((index*100)/(size(cards)-1)*(level_width-360))/100
			where ypos = level_height/2

			where card_obj = find(level.chars, value is obj card and value.card_type = context.value.card) where uuid = value.card._uuid
			
			)
	    where cards = game.current_choice.card_options,

			set_widgets(map(buttons, {
				type: 'button',
				padding: [25, 15],
				resolution: 'normal',
				align_h: 'center',
				x: 180 + ((index*100)/(size(buttons)-1)*(level_width-360))/100,
				y: level_height - 200,
				label: {type: 'label', text: value.text, font: lib.font.regular_font, size: 16, color: 'white'},
				color_scheme: lib.citadel.button_color_scheme,
				on_click: q(find(level.chars, value.type = 'citadel_controller').option_clicked) + '(' + index(game.current_choice.options, value) + ', ' + str(value.tag) + ')',
				on_mouse_enter: q(find(level.chars, value.type = 'card').on_mouse_enter)
			}))
		where buttons = filter(game.current_choice.options, value is class choice.button),

		spawn('label', {
			_font_size: lib.gui.py(34),
			_text: [if(_controller.myturn, game.current_choice.text, 'Waiting for your opponent to ' + game.current_choice.opponent_text)],
			zorder: 20000,
			_color: [1,1,1,1],
			_bg_color: [0.0, 0.0, 0.0, 0.7],
			x: lib.gui.py(20),
			y2: level_height - lib.gui.py(20),
			_halign: 'left',
			_pad_left: lib.gui.py(8),
			_pad_right: lib.gui.py(8),
		}, [
			set(_label, child),
		])

] asserting game.current_choice != null",

on_process: "[
	_mm.process_matchmaking_connection(),
	_controller.process_connection(),
]",


animation: {
	id: "normal",
	image: "dark-solid.png",
	x: 0,
	y: 0,
	w: 3,
	h: 3,
	frames: 1,
	duration: 1000,
},

}
