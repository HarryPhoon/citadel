{
id: "level_display",
is_strict: true,

properties: {
	lvl: { type: "int" },
	xp: { type: "int" },
	xp_needed: { type: "int" },

	school: { type: "int" },

	player_num: { type: "int" },

	order: { default: -1 },

	_font_size: 12,

	_render_key: "[level, xp, xp_needed, school, player_num]",

	_bg_color: "[decimal,decimal,decimal] :: [[0,0,0], [0.85,0.56,0.14], [0.71,0.32,0.28],[0.49,0.65,0.55],[0.40,0.24,0.30],[0.08,0.37,0.40]][school]",

	render: "def(int ww, int hh) ->commands [
	set(animation, {
		id: 'fbo',
		image: 'fbo',
		x: 0, y: 0, w: ww, h: hh,
		scale: 1, frames: 1,
		fbo: query_cache(global_cache(12), _render_key, c.render(ww, hh, [
			c.save(),
			c.scale(ww, hh),
			c.move_to(0.2, 0.5),
			c.line_to(0.2 + 0.25*0.8, 0),
			c.line_to(0.2 + 0.75*0.8, 0),
			c.line_to(1, 0.5),
			c.line_to(0.2 + 0.75*0.8, 1),
			c.line_to(0.2 + 0.25*0.8, 1),

			c.set_source_rgba(_bg_color[0], _bg_color[1], _bg_color[2], 1),
			c.fill(),

			c.restore(),

			c.save(),

			c.set_font(lib.citadel.sans_font),
			c.set_font_size(_font_size),
			c.translate(16, 40),
			c.rotate(-3.14*0.34),
			c.text_path('LEVEL' + str(lvl)), 
			c.set_source_rgba(1, 1, 1, 1),
			c.fill(),

			c.restore(),

			c.save(),

			c.translate(42, 12),
			c.scale(0.07),
			c.draw_svg('images/icons/school-' + SCHOOL_NAMES[school] + '.svg'),

			c.restore(),

			c.save(),

			] +

			(fold(map(range(xp_needed), [
				c.arc_negative(center_x, center_y, radius*ww, 0, 2*3.15),
				c.arc(center_x, center_y, (radius+0.01)*ww, 0, 2*3.15),
				c.set_source_rgba(1, 1, 1, 1),
				c.fill(),
			] + if(xp > index, [
				c.arc(center_x, center_y, radius*ww, 0, 2*3.15),
				c.set_source_rgba(1, 1, 1, 1),
				c.fill(),
			], [])
				where center_y = 0.75*hh
				where center_x = right*(index+1.0)/(xp_needed+1.0) + left*(xp_needed - index)/(xp_needed+1.0)
				where radius = if(xp_needed <= 3, 0.05, 0.05*(3.0/xp_needed))
			), a + b)
			  where left = ww*0.3
			  where right = ww*0.9
			) + [

			c.restore(),
		]) where c = canvas()),
	}),
	]",
},

on_update: "[render(100, 80)]",
}
