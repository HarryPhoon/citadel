{
	id: "devotion_connector",
	is_strict: true,

	properties: {

		enemy: { type: "bool" },

		height: {
			type: "int",
			set: "if(value != _data, [set(_data, value); render() ])",
		},

		render: "def() ->commands
			set(animation, {
				id: 'normal',
				image: sprintf('gui/school-devotion-link-%s.png', if(enemy, 'silver', 'gold')),
				x: 0, y: 512-frame_height-2, w: 6, h: frame_height,
				frames: 1, scale: scaling,
				no_remove_alpha_borders: true,
			})

			where frame_height = round_to_even(min(510, int(height/scaling)))
			where scaling = 1.0
		",

		_base: { type: "null|obj image_widget" },
	},

	on_create: "render();
	if(enemy, [
		set(facing, -1),
		set(upside_down, 1)
	]);

	remove_object(_base);
	
	spawn('image_widget', {
		zorder: me.zorder+1,
		mid_x: me.x + lib.gui.py(if(enemy, -10, 16)),
		mid_y: me.y2 + lib.gui.py(if(enemy, 40, -20)),
		image: sprintf('gui/school-devotion-base-%s.png', if(enemy, 'silver', 'gold')),
		image_area: [0,0,128,128],
		draw_width: lib.gui.py(84),
		draw_height: lib.gui.py(84),
	}, [
		set(_base, child),
	])",

	on_being_removed: "remove_object(_base)",

	on_type_updated: "fire_event('create')",

}
