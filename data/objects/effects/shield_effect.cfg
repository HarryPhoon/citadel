{
	id: "shield_effect",
	prototype: ["creature_effect"],
	is_strict: true,

	animation: [

	{
		id: "normal",
		image: "effects/shield-effect.png",
		x: 2, y: 2, w: 151, h: 178,
		frames: 7,
		pad: 3,
		duration: 4,
		no_remove_alpha_borders: true,
		force_no_alpha: true,
	},

	{
		id: "permanent",
		image: "effects/shield-effect.png",
		x: 926, y: 2, w: 151, h: 178,
		frames: 1,
		pad: 3,
		duration: -1,
		no_remove_alpha_borders: true,
		force_no_alpha: true,
	},

	{
		id: "erase",
		image: "effects/shield-erase.png",
		x: 2, y: 2, w: 159, h: 141,
		frames: 5,
		duration: 4,
		pad: 3,
		no_remove_alpha_borders: true,
		force_no_alpha: true,
	},

	],

	on_create: "[
		set(mid_x, creature.mid_x),
		set(mid_y, creature.mid_y + lib.gui.py(50)),

		set(rotate, 0),
		add(y, -lib.citadel.py(60)),
		set(scale, 0.7*tile_height/140.0) where tile_height = if(tile, tile.hex_height, lib.citadel.py(128)) where tile = find(level.chars, value is obj tile)
	]",

	on_end_erase_anim: "remove_object(me)",
	on_end_anim: "[
	  set(animation, 'permanent'),
	  if(animation = 'normal' and _flash = null,
		spawn('shield_flash_effect', {
			mid_x: mid_x,
			mid_y: mid_y,
			zorder: zorder+200,
			shield_effect: me,
			targets: [],
	    }, [
			set(_flash, child)
		])
	  )
	]",

	on_process_permanent: "
	[
		set(mid_x, creature.mid_x),
		set(mid_y, creature.mid_y + lib.gui.py(-10)),

		if((_last_flash = null or cycle - _last_flash > 600) and 1d600 = 1, flash()),
	]
	",

	on_being_removed: "[
		remove_object(_flash),
		set(_flash, null),
	]",

	properties: {
		_flash: { type: "null|obj shield_flash_effect" },
		_last_flash: { type: "int|null" },

		flash: "def() ->commands
		if(_flash != null,
		[
			set(_flash.animation, 'flash'),
			set(_last_flash, cycle),
		])
		",
		remove: "def() ->commands [
			set(animation, 'erase'),
			remove_object(_flash),
			set(_flash, null),
		]",

		mouse_enter_creature: "def() ->commands
			if(_last_flash = null or cycle - _last_flash > 300, flash())
		",
	}
}
