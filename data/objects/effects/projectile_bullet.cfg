{
	id: "projectile_bullet",
	is_strict: true,

	animation: {
		id: "bullet",
		image: "effects/bullet.png",
		x: 0,
		y: 0,
		w: 2,
		h: 2,
	},

	properties: {
		_previous_pos: { type: "null|[int,int]" },
		previous_pos: "[int,int] <- _previous_pos",
		target_x: { type: "int" },
		target_y: { type: "int" },
		velocity: { type: "decimal", init: "lib.gui.dpy(10.0)" },
	},

	events: {
		create: "[
			set(alpha, 64),
			set(_previous_pos, [mid_x, mid_y]),

			animate(me, {
				mid_x: target_x,
				mid_y: target_y,
			}, {
				duration: max(1, int(dist/velocity)),
				on_complete: remove_object(me),
			}) where dist = sqrt((target_x-mid_x)*(target_x-mid_x) + (target_y-mid_y)*(target_y-mid_y)),
		]",

		draw: "[
			set(blur, map(range(nblur),
				blur_object({
					alpha: 64,
					mid_x: mix(previous_pos[0], mid_x, ratio),
					mid_y: mix(previous_pos[1], mid_y, ratio),
				}, {
					duration: 1,
				})
				where ratio = value/decimal(nblur)
			)) where nblur = int(velocity/2),
			set(_previous_pos, [mid_x, mid_y]),
		]",
	},
}
