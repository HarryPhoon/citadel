//   Special credit to Phanterm for these card concepts.
//
//   Void = Aether + Entropia = Void. Crypt focus. Loves spells that
//   transport creatures to and from the crypt -- any crypt...
//
//     CARD                  |   CODE |    ART | FLAVOR |
//                           |        |        |   TEXT |
//     ----------------------+--------+--------+--------+
//     Shriequalize          |++++++++|........|        |
//     Revel Witch           |--------|--------|        |
//     Reanimate             |--------|--------|        |
//     Catacombs             |++++++++|--------|        |
//     Bury Alive            |++++++++|--------|        |
//     Ghost Writer          |++++++++|--------|        |
//     Magic Doll            |++++++++|--------|        |
//     Grave Promotion       |++++++++|++++++++|        |
//     Vengeful Posession    |        |        |        |
//     Infernal Bard         |++++++++|--------|        |
//     Soul Stitch           |++++++++|++++++++|        |
//     Lost Affluent         |        |        |        |
//     Imperial Carronade    |        |        |        |
//     False Moon Rising     |        |        |        |
//     Zangried, First Dead  |--------|--------|        |
//
//   Key
//
//     '      ' -+-> CODE -+-> Completely pending.
//               `-> ART -'
//
//     '......' -+-> CODE -> Sketch, or even simply a skeleton.
//               `-> ART -> **Super early** placeholder art.
//
//     '------' -+-> CODE -> Started, early WIP, absolutely not finished.
//               `-> ART -> _Complete_ placeholder art.
//
//     '++++++' -+-> CODE -> Completed, or late WIP. Subject to changes by
//               |           peers.
//               `-> ART -> Sketch???
//
//     '######' -+-> CODE -> Accomplished, or post peer review.
//               `-> ART -> Final art???

{

"Shriequalize": {
	name: "Shriequalize",
	set: "cascadence",
	rarity: 0,
	type: "spell",
	is_response: true,
	cost: 1,
	loyalty_cost: 1,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	portrait: "balance.png",
	rules: "Target creature's power and toughness become equal to the lowest of the two values. If Shriequalize is in your crypt, return this card to your hand after casting it and increase the cost by 1.",
	possible_targets: "all_creatures_as_possible_targets",
	on_play: "def(class game_state game, class message.play_card info) ->commands
	[
		set(creature.attack, new_val),
		set(creature.life, new_val),
		if(find(crypt, value.name='Shriequalize' and value._uuid != info.card_uuid), [
			game.crypt.erase_card(info.card),
			add(player.hand, [ lib.citadel.create_card('Shriequalize', {cost: (info.card.cost+1)}) ]),
		]) asserting game.crypt.contains(info.card)
		   where crypt=game.crypt.cards_of(player)
		   where player=game.players[info.player_index],
	] asserting info.card != null
	  asserting size(info.targets) = 1
	  where new_val=min(creature.attack, creature.life)
	  where creature=game.creature_at_loc_or_die(info.targets[0])",
	},

"Revel Witch": {
	name: "Revel Witch",
	set: "cascadence",
	rarity: 0,
	type: "creature",
	portrait: "siren.png", //   TODO custom art.
// 	artist: "artist_TODO",  //   TODO
// 	flavor_text: "flavor_text_TODO",  //   TODO
	hue_shift: 0.72,
	cost: 2,
	loyalty_cost: 1,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	creature: {
		game_sounds: "magic_user",
		tags: ["Human", "Witch"],
		attack: 3,
		life: 3,
		move: 1,
		passive_abilities: [{
			name: "Soul Siphon",
			rules: "Revel Witch gets +1/+1 for each creature in your crypt.",
			static_effect: "def(class game_state game, class creature creature) ->commands [
				creature.apply_static_effect('attack', deaths),
				creature.apply_static_effect('life_shield', deaths),
				// helper code for Cheat Death
				if(creature.effects_tracking['Cheat Death'], creature.add_ability_static_duration('Indestructible')),
			] where deaths=size(player.creatures_in_crypt)
			  where player=game.players[creature.controller]",
		}],
		triggered_abilities: [{
			hidden_on_card: true,
			hidden_on_creature: true,
			on_deal_damage: "def(class creature creature, class game_state game, class creature target, int damage, null|class animation.creature_attack_hint hint=null) ->commands [
				set(creature.effects_tracking['Cheat Death'], false),
			]",
			on_attacked: "def(class creature creature, class game_state game, class creature attacker, int damage) ->commands [
				set(creature.effects_tracking['Cheat Death'], false),
			]",
			on_card_played: "def(class creature creature, class game_state game, class player player, class message.play_card info) ->commands [
				if(info.card != null and info.card.type = 'spell', set(creature.effects_tracking['Cheat Death'], false)),
			]",
		}],
		activated_abilities: [{
			name: "Cheat Death",
			rules: "You may obliterate a creature in your crypt to become momentarily <i>Indestructible</i>. The effect ends after the next attack or spell.",
			cost: 0,
			once_per_turn: true,
			school: "@eval [AETHER, ENTROPIA]",
			tags: ["Void"],
			ai_value: 1.2,
			portrait: "life-flow.png",
			hue_shift: 0.188236,
			luminance_multiplier: 1.235292,
			saturation_multiplier: 1.482352,
			is_response: true,
			sound_resolve: "gameplay/spell_bless",
			possible_targets: "def(class game_state game, class creature creature, [Loc] targets) ->[Loc]|null
				if(game.players[creature.controller].creatures_in_crypt, null, [])",
			custom_reason_cannot_cast: "def(class game_state game, class creature avatar) ->null|string
				if(game.players[avatar.controller].creatures_in_crypt, null, 'No creatures in your crypt')",
			on_play: "def(class game_state game, class message.play_card info) ->commands 
			[
// TODO; animation hint
//					_text: [ sprintf('<b>%s</b> %s', target.name, 
//					           if(steal, 'obliterated from enemy crypt', 'obliterated from your crypt'))
//					       ],
// TODO; choose the card to obliterate from crypt (or enemy's!)
				game.crypt.erase_creature(target),
				set(creature.effects_tracking['Cheat Death'], true)
			] where target = if(steal, steal[0], deads[0]) asserting size(deads) > 0
			  where steal = filter(deads, value.controller != creature.controller)
			  where deads = game.players[creature.controller].creatures_in_crypt 
			  where creature = info.get_creature(game)",
		}],
	},
},

"Reanimate": {
	name: "Reanimate",
	set: "cascadence",
	rarity: 0,
	type: "spell",
	is_instant: true,
	cost: 3,
	loyalty_cost: 1,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	portrait: "zombie-c.png",
	rules: "Return target creature from your crypt to your hand; its type will be <i>Undead</i> in addition to its other types.",
	possible_targets: "def(class game_state game, class creature creature, [Loc] targets) ->[Loc]|null
		if(game.players[creature.controller].creatures_in_crypt, null, [])",
	custom_reason_cannot_cast: "def(class game_state game, class creature avatar) ->null|string
		if(game.players[avatar.controller].creatures_in_crypt, null, 'No creatures in your crypt')",
	on_play: "def(class game_state game, class message.play_card info) ->commands
		if(info.choices,
		([
			[ set(entry.owner, info.player_index),
			  if('Undead' not in ( entry.card.tags + creature_tags ),
				set(entry.card, lib.citadel.create_card(entry.card.name, {
					saturation_multiplier: 0.0
				}))
			  ); [[
			  entry.card.modify_creature('tags', creature_tags+['Undead']),
			  entry.card.modify_creature('triggered_abilities', creature_abil+[
				construct('triggered_ability', {
					name: 'Zombified',
					hidden_on_card: true,
					hidden_on_creature: true,
//					ignore_hidden_on_creature_on_turn_when_summoned: true,
					icon: 'passive-bad.png',
					notes: 'The Reanimate spell has brought this card back from your crypt.',
					rules: 'This creature is considered <i>Undead</i>.',
					on_summoned: 'def(class creature creature, class game_state game) ->commands add(creature.abilities, [q(Zombified)])'
				})])
			];[
			  game.crypt.return_to_hand(entry.card)
			]]] asserting entry and entry.card.creature and entry.card.creature_object
		] where creature_tags = [string]<- if(entry.card.creature.tags, entry.card.creature.tags, [])
		  	asserting entry and entry.card.creature and entry.card.creature_object
		  where	creature_abil = entry.card.creature_object.triggered_abilities
		  	asserting entry and entry.card.creature and entry.card.creature_object
		  where entry=game.crypt.get_entry(info.choices[0])
		) asserting size(info.choices)=1 | info.choices,
		// else
		if(entries, game.set_current_choice({
			card: me,
			info: info,
			text: 'Choose which creature to return to your hand',
			opponent_text: 'select a creature from their crypt',
			options: map(entries, construct('choice.card', {
				card: value.card,
				tag: (class creature<- value.creature).summon_id,
			}) asserting value ),
		})) 
		) where entries=[game.crypt.get_entry(x) | x<- player.creatures_in_crypt]
		  where player=game.players[info.player_index]",

},


"Catacombs": {
	name: "Catacombs",
	set: "cascadence",
	type: "creature",
	is_response: false,
	cost: 1,
	rarity: 0,
	loyalty_cost: 0,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	portrait: "grave-stalker.png",  //   TODO custom art.
// 	artist: "artist_TODO",  //   TODO
// 	flavor_text: "flavor_text_TODO",  //   TODO
//	rules: "(See abilities)",
	ai_flags: "@eval [AI_CARD_BUILD_DEVOTION]",
	creature: {
		abilities: ["Fragile"],
		is_land: true,
		portrait: "grave-stalker.png",
		triggered_abilities: [{
			name: "Void Harvest",
			icon: 'trigger-death.png',
			rules: "If a creature dies while on this land, sacrifice Catacombs; it gives +3 mana pool while in your crypt if sacrificed.",
			on_another_creature_died: "def(class creature creature, class game_state game, class creature victim) ->commands [
				if(victim.loc = creature.loc and not victim.is_land, set(creature.sacrificed, true)),
// TODO: needs some animation for the land sacrifice
			]",
			on_die: "def(class creature creature, class game_state game) ->commands
			if(creature.sacrificed, [[ add(avatar.passive_abilities,
				[ construct('passive_ability', {
					name: 'Void Harvest',
					static_effect: q^def(class game_state game, class creature avatar) ->commands
					[ if(active, player.apply_static_effect(
						(def(class player p, map info) ->commands add(p.base_income, int <- info.amount)),
						(def(class player p, map info) ->commands add(p.base_income, -1 * (int <- info.amount))),
						{amount: 3}
					))] where active=find(
						game.crypt.lands_of(player),
						value.summon_id = ^+ creature.summon_id +q^
					    )
					    where player=game.players[avatar.controller]^,
				})])
			] where avatar = player.creature_avatar | player <- game.players,
				player.player_index = creature.controller ])",
		}],
		passive_abilities: [{
			name: "Void Drain",
			icon: 'plus-mana.png',
			rules: "Summoner loses -1 mana pool",
				static_effect: "def(class game_state game, class creature creature) ->commands
				if(creature.summoner >= 0,
				   game.players[creature.summoner].apply_static_effect(
					(def(class player p, map info) ->commands [
						add(p.base_income, -1);
						set(p.resources, min(p.resources, p.base_income))
					]),
					(def(class player p, map info) ->commands add(p.base_income, 1)),
					{}))
				",

		}],
	},

},

"Bury Alive": {
	name: "Bury Alive",
	set: "cascadence",
	type: "spell",
	is_response: false,
	cost: 4,
	rarity: 0,
	loyalty_cost: 1,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	portrait: "curse.png",  //   TODO custom art.
// 	artist: "artist_TODO",  //   TODO
// 	flavor_text: "flavor_text_TODO",  //   TODO
	rules: "Destroy target creature. It returns to its owner's hand at the end of the turn.",
	ai_flags: ["@eval AI_CARD_NEVER_TARGET_FRIENDS"],
	possible_targets: "all_creatures_as_possible_targets",
	on_play: "def(class game_state game, class message.play_card info) ->commands
	[
		set(creature.destroyed, true),
		add(game.players[info.player_index].creature_avatar.triggered_abilities,
		[ construct('triggered_ability', {
			name: 'Bury Alive',
			once: true,
			unique_id: trigger_id,
//			particle_effects for owner avatar until end of turn? // TODO
			on_end_turn: q^def(class creature avatar, class game_state game) ->commands [
				if(corpse, game.crypt.return_to_hand(corpse)),
				trigger(avatar),
			] where corpse = game.crypt.get_creature(^+ creature.summon_id + q^)
                        ^,
		  })
		]),
	] where creature = game.creature_at_loc_or_die(info.targets[0]),
	        trigger_id = generate_uuid()",
},

"Ghost Writer": {
	name: "Ghost Writer",
	set: "cascadence",
	rarity: 0,
	type: "creature",
	portrait: "mal-ravanal.png", //   TODO custom art.
// 	artist: "artist_TODO",  //   TODO
// 	flavor_text: "flavor_text_TODO",  //   TODO
	cost: 5,
	loyalty_cost: 0,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	creature: {
		game_sounds: "ghostly_creature",
		tags: ["Undead"],
		attack: 3,
		life: 7,
		move: 1,
		triggered_abilities: [{
			name: "Tome of the Tomb",
			icon: 'trigger-death.png',
			rules: "While Ghost Writer is in your crypt, the cost of Undead creatures is 1 less.",
			on_die: "def(class creature creature, class game_state game) ->commands
			[[add(avatar.passive_abilities,
				[ construct('passive_ability', {
					name: 'Tome of the Tomb',
//			particle_effects while a Ghost Writer is in your crypt? // TODO			
					static_effect: q^def(class game_state game, class creature avatar) ->commands
					[ if(active, player.add_static_cost_filter(
						construct('cost_filter', {
							filter_cost: 'def(class player p, class card_base card, int cost) ->int
							if(card.creature_object and (q(Undead) in card.creature_object.tags),
								max(0, cost - 1),
								cost
							)',						
						})
					))] where active=count(player.creatures_in_crypt, value.summon_id=^+ creature.summon_id +q^),
					    where player=game.players[avatar.controller]^,
				})])
			] where avatar = player.creature_avatar | player <- game.players]",
		}],
	},
},

"Magic Doll": {
	name: "Magic Doll",
	set: "cascadence",
	type: "creature",
	is_response: false,
	cost: 4,
	rarity: 0,
	loyalty_cost: 0,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	portrait: "dark-effigy.png",  //   TODO custom art.
// 	artist: "artist_TODO",  //   TODO
// 	flavor_text: "flavor_text_TODO",  //   TODO
	rules: "Destroy target spell from your crypt. A golem is summoned with X attack and life, where X equals the maximum cost of the spell",
	is_channeled: false,
	possible_targets: "def(class game_state game, class creature avatar, [Loc] targets) ->[Loc]|null
		if(size(targets) = 1, null,
		   not find(game.crypt.cards_of(player), value.type='spell'), [],
		   player.summoning_locs(game, construct('creature', {name: 'Doll', school: [AETHER, ENTROPIA] }))
		 ) where player=game.players[avatar.controller]",
	custom_reason_cannot_cast: "def(class game_state game, class creature avatar) ->null|string
		if(not find(game.crypt.cards_of(player), value.type='spell'), 'No spells in your crypt',
		   null) where player=game.players[avatar.controller]",
	on_play: "def(class game_state game, class message.play_card info) ->commands
		if(current_choices,
			do_summons(game, info),
			// else
			if(crypt_spells, game.set_current_choice({
				card: me,
				info: info,
				text: 'Choose a spell from your crypt',
				num_choices: 1,
				options: map(crypt_spells, construct('choice.card', {
					card: value,
					tag: index,
				})),
			}))
		) where crypt_spells=filter(game.crypt.cards_of(player), value.type='spell')
		  where player=game.player_obj,
			current_choices = info.choices or []
	",
	creature: {
		game_sounds: "green_adder",
		attack: 0,
		life: 0,
		tags: ['Golem','Construct'],
		on_summoned: "def(class game_state game) ->commands [
			set(attack, cost),
			set(life, cost),
			set(abilities, []),
//		] where cost=player.calculate_cost(crypt_spells[choices[0]])
		] where cost=(card.cost + (card.loyalty_cost * size(card.school)))
		  where card=crypt_spells[choices[0]]
			asserting (size(choices) = 1)
			asserting (choices != null)
		  where crypt_spells=filter(game.crypt.cards_of(player), value.type='spell')
		  where player=game.player_obj",
	}
},

"Grave Promotion": {
	name: "Grave Promotion",
	set: "cascadence",
	rarity: 1,
	type: "spell",
	is_instant: false,
	cost: 3,
	loyalty_cost: 0,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	portrait: "doomsday-crypt.png",
	rules: "Target living creature becomes <i>Undead</i>. You gain 4 life.",
	possible_targets: "def(class game_state game, class creature creature, [Loc] targets) ->[Loc]|null
		if(unfiltered != null, filter(unfiltered, 'Undead' not in game.creature_at_loc_or_die(value).tags))
		where unfiltered=me.all_creatures_as_possible_targets(game, creature, targets)",
	on_play: "def(class game_state game, class message.play_card info) ->commands
		if(creature,
		[
			add(game.players[info.player_index].life, 4),
			add(creature.abilities, ['Zombified']),
			add(creature.passive_abilities, [construct('passive_ability', {
					name: 'Zombified',
					icon: 'passive-bad.png',
					notes: 'The Grave Promotion spell has been cast.',
					rules: 'This creature is considered <i>Undead</i>.',
					hidden_on_creature: true,
					static_effect: 'def(class game_state game, class creature creature) ->commands
						creature.add_ability_static_duration(q(Zombified))
					',

			})])
		]) where creature=game.creature_at_loc(info.targets[0])
		   asserting (info.targets and size(info.targets)=1) | info.targets",
},

"Infernal Bard": {
	name: "Infernal Bard",
	set: "cascadence",
	type: "creature",
	is_response: false,
	cost: 4,
	rarity: 1,
	loyalty_cost: 0,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	portrait: "stoic.png",  //   TODO custom art.
	hue_shift: 0.847058,
	luminance_multiplier: 0.94118,
	saturation_multiplier: 1.682352,
// 	artist: "artist_TODO",  //   TODO
// 	flavor_text: "flavor_text_TODO",  //   TODO
	is_channeled: false,
	creature: {
		game_sounds: "spellcaster_male",
		attack: 5,
		life: 4,
		tags: ['Demon'],
		activated_abilities: [{
			name: "Control Undead",
			rules: "Take control of target Undead creature and give it +1/+1. When Infernal Bard dies, his controlled creature also dies.",
			cost: 0,
			once_ever: true,
			school: "@eval [AETHER, ENTROPIA]",
			tags: ["Void"],
			ai_value: 1.2,
			portrait: "life-flow.png",
			hue_shift: 0.188236,
			luminance_multiplier: 1.235292,
			saturation_multiplier: 1.482352,
			is_response: false,
			sound_resolve: "gameplay/blink",
			possible_targets: "def(class game_state game, class creature creature, [Loc] targets) ->[Loc]|null
				if(unfiltered != null, filter(unfiltered, 'Undead' in game.creature_at_loc_or_die(value).tags))
				where unfiltered=me.all_creatures_as_possible_targets(game, creature, targets)",
			on_play: "def(class game_state game, class message.play_card info) ->commands 
			[
				// TODO: add animation
				set(creature.effects_tracking['Control Undead'], target.summon_id),
				add(target.attack, 1),
				add(target.life, 1),
				if(target.controller != creature.controller, [
					set(target.controller, creature.controller),
					target.exhaust(),
				]),
			] where target = game.creature_at_loc_or_die(info.targets[0]),
				creature = info.get_creature(game)",
		}],
		triggered_abilities: [{
			hidden_on_card: true,
			hidden_on_creature: true,
			on_die: "def(class creature creature, class game_state game) ->commands [
				if(affected,
					if(target and target.alive, [
						add(game.animation_hints, [construct('animation.creature_particle_hint', { type: 'creature_particle_hint', particle_type: 'dark_sacrifice', creature: target, target: creature, })]),
						set(target.sacrificed, true)
					]) where target=game.creature_by_summon_id(int<- affected)
				) where affected=creature.effects_tracking['Control Undead']
			]",
		}],
	}
},

"Soul Stitch": {
	name: "Soul Stitch",
	set: "cascadence",
	rarity: 1,
	type: "spell",
	is_instant: true,
	cost: 0,
	loyalty_cost: 2,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	portrait: "ilz-riddlespinner.png",
	rules: "Put the top 3 cards of your deck into your crypt. You restore 3 mana.",
	sound_resolve: "creatures/kill-dark-emissary.wav",
	on_play: "def(class game_state game, class message.play_card info) ->commands
		[ // FIXME: none of these animations are showing... update: Only draw_cards is showing
			if(info.choices=null, [
				player.gain_mana_from_card_overload(game, me, 3),
				set(info.choices, [n+size(player.hand)| n<- [2,1,0]]),
				player.draw_cards(game, 3);
				game.add_delayed_commands(; me.on_play(game, info))
			], // else if
			info.choices=[], null,
			// else
		 	[game.add_animation_hint(construct('animation.discard_card_hint', {
				player: info.player_index,
				card: card })),
			  if(card.creature, game.crypt.spawn_cards(info.player_index, [card.name]),
				game.crypt.add_card(card, info.player_index)),
			  set(player.hand, filter(player.hand, value != card)),
			  set(info.choices, info.choices[1:]);
			  bind_command(me.on_play, game, info)
			] where card=player.hand[info.choices[0]]
			  asserting info.choices
			) // end if
		] where player=game.players[info.player_index]
	",
},

"Zangried, the First Dead": {
	name: "Zangried, the First Dead",
	set: "cascadence",
	rarity: 3,
	type: "creature",
	portrait: "angel-of-death.png", //   TODO custom art.
// 	artist: "artist_TODO",  //   TODO
 	flavor_text: "*Cackles maniacally*",
	cost: 8,
	loyalty_cost: 2,
	school: "@eval [AETHER, ENTROPIA]",
	tags: ["Void"],
	hero: true,
	creature: {
		game_sounds: "soldier_mace",
		tags: ["Avatar", "Angel"],
		abilities: ['Cover'],
		attack: 13,
		life: 13,
		move: 1,
		triggered_abilities: [{
			name: "Omnigrave",
			icon: 'passive-generic.png',
			rules: 'Spells you cast that say "your crypt" become "any crypt".',
			on_summoned: "def(class creature creature, class game_state game) ->commands [
				if(creature.controller >= 0, add(game.players[creature.controller].omnigrave, 1))
			]",
			on_die: "def(class creature creature, class game_state game) ->commands [
				if(creature.controller >= 0, add(game.players[creature.controller].omnigrave, -1))
			]",
			on_reach_endzone: "def(class creature creature, class game_state game) ->commands [
				if(creature.controller >= 0, add(game.players[creature.controller].omnigrave, -1))
			]",
//			static_effect: "def(class game_state game, class creature creature) ->commands [
//				game.players[creature.controller].apply_static_effect(apply_fcn, remove_fcn, {})
//			] where apply_fcn = def(class player player, map m) ->commands add(player.omnigrave, 1),
//				remove_fcn = def(class player player, map m) ->commands add(player.omnigrave, -1)",
		}],
	},
},

}
